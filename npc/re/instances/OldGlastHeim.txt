//===== rAthena Script =======================================
//= Old Glast Heim
//===== Description: =========================================
//= Discover the history of events that took place in the
//= Glast Heim castle and how it ended up in ruins.
//===== Changelogs: =================================
//= 1.0 First version. [Euphy, Ziu, Heris]
//=     This is a custom version, and may contain bugs.
//= 1.1 Bug fixes; removed redundant OnInstanceInit scripts.
//= 1.2 Add NPC Hugin's Follower [exneval]
//=     NPC that give access to Glast Heim Nightmare Mode.
//= 1.3 Add some NPCs placeholder. [exneval]
//= 1.4 Update to its official text. [exneval]
//=     Support merchant, enchant, socket features.
//= 1.5 idAthena merge. Removed Hugin's Follower NPCs. [Secretdataz]
//=     Changed `set` calls to direct assignments.
//=     Cleaned up the script.
//=     TODO: Confirm Hugin NPC's code flow.
//= 1.6 Cleaned up the dialogue. [Aleos]
//============================================================

glast_01,204,273,5	script	华金	4_M_SAGE_C,{
	if(BaseLevel < 130) {
		mes "[华金]";
		mes "等你再提升提升自己，我才可能对你感兴趣，冒险者。";
		mes "先升到130级吧！";
		close;
	}
	if (isbegin_quest(12316) == 0) {
		mes "[华金]";
		mes "很久以前，这座城堡并不是这样的。";
		next;
		mes "[华金]";
		mes "啊...不好意思，我在陌生人面前自顾自的说什么呢。";
		next;
		mes "[华金]";
		mes "我叫华金，我是专门研究时空的次元裂缝的旅行者。";
		next;
		select("这里有那样的东西么？");
		mes "[华金]";
		mes "你对克雷斯特汉姆古城的过去不好奇么？";
		next;
		mes "[华金]";
		mes "曾经居住在这的国王叫做舒米彻，在卢恩米德加尔特王国的历史中，这是一位暴君。";
		next;
		mes "[华金]";
		mes "但这是真相么？也许只有时间旅行能揭示。";
		next;
		if(select("我对历史不感兴趣。:哦，听起来很有意思，有什么发现么？") == 1) {
			mes "[华金]";
			mes "真的？那等你有兴趣了再来吧。";
			close;
		}
		mes "[华金]";
		mes "实际上，我发现了时空裂缝，可以前往古代的克雷斯特汉姆，但我有点惧怕进去。";
		next;
		mes "[华金]";
		mes "对了！你是冒险者吧，也许你会有兴趣去冒险。";
		next;
		mes "[华金]";
		mes "你想经历时间旅行么？";
		next;
		if(select("算了:是的，当然！") == 1) {
			mes "[华金]";
			mes "真的？不过跳进时空裂缝中确实也挺危险的。";
			close;
		}	
		mes "[华金]";
		mes "我就知道你愿意，请回来之后告诉我你的见闻！";
		setquest 12316;
		completequest 12316;
	}
	else {
		mes "[华金]";
		mes "准备好进去了？";
		if (isbegin_quest(12322) == 1)
			erasequest 12322;
	}
	next;
	mes "[华金]";
	mes "你说我看起来面熟？这不可能，我们不是刚刚第一次见面么？";
	mes "接下来你想怎么做？";
	next;
	switch( checkquest(12317,PLAYTIME) ) {
	case -1:
		.@party_id = getcharid(1);
		.@p_name$ = getpartyname(.@party_id);
		.@md_name$ = "Old Glast Heim";

		if (!instance_check_party(.@party_id)) {
			mes "[华金]";
			mes "你的队伍成员呢？";
		}
		if (is_party_leader() == true)
			.@menu$ = "Generate Time Gap";
		else {
			mes "[Hugin]";
			mes "Have we met before? No way. It's my first time seeing you. What do you want?";
		}
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		switch( select( .@menu$, "Enter Old Glast Heim", "Cancel" ) ) {
		case 1:
			switch( instance_create(.@md_name$) ) {
			case -3:
				dispbottom "记忆地牢, '古城的记忆' 已经被创建过了.",0xFFFFFF;
				close;
			case -4:
			case -2:
			case -1:
				mes "队伍名称: " + getpartyname( getcharid(1) );
				mes "队    长: " + strcharinfo(0);
				mes "^0000ff" + .@md_name$ + "^000000 - 创建副本失败.";
				close;
			}
			mes "[华金]";
			mes "时间裂缝已经打开了，准备好了再来吧。";
			close;
		case 2:
			switch( instance_enter(.@md_name$) ) {
			case IE_OTHER:
				mes "[华金]";
				mes "发生一个未知错误。";
				close;
			case IE_NOINSTANCE:
				mes "[华金]";
				mes "副本进度 古代的克雷斯特汉姆 不存在。";
				mes "队长还没有建立副本进度。";
				close;
			case IE_NOMEMBER:
				mes "[华金]";
				mes "只有注册过的成员可以进入副本 古代的克雷斯特汉姆(困难)。";
				close;
			case IE_OK:
				mapannounce "glast_01", "" + getpartyname( getcharid(1) ) + " party member " + strcharinfo(0) + " enters the " + .@md_name$ + ".", bc_map,0x00FF99;
				setquest 12317;// Trace of Time Travel
				// warp "1@gl_k",150,20;
				end;
			}
		case 3:
			close;
		}
	case 0:
	case 1:
		mes "[华金]";
		mes "噢，天啊。";
		mes "你的身上还有副本的印记。这种情况下，你是不可以再次进入副本的。";
		next;
		mes "[华金]";
		mes "你应该多休息一下，晚些时候再来。";
		close;
	case 2:
		mes "^0000ff古代的克雷斯特汉姆的印记已被清除。现在可以再次与华金对话了。^000000";
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		erasequest 12317;
		close;
	}
}

// Warps
1@gl_k,96,80,0	warp	#2Control	2,2,1@gl_k,80,80
1@gl_k,90,80,0	warp	#2Control2	2,2,1@gl_k,105,80
1@gl_k,202,79,0	warp	#3Control	2,2,1@gl_k,215,79
1@gl_k,206,79,0	warp	#3Control2	2,2,1@gl_k,195,79
1@gl_k,227,216,0	warp	#4Control	2,2,1@gl_k,215,216
1@gl_k,222,216,0	warp	#4Control2	2,2,1@gl_k,233,216
1@gl_k,150,284,0	warp	#2F Entrance	2,2,2@gl_k,150,46
2@gl_k,149,32,0	warp	#1 Control	2,2,1@gl_k,150,270
2@gl_k,145,123,0	warp	#22 Control	2,2,2@gl_k,126,123
2@gl_k,136,122,0	warp	#22 Control2	2,2,2@gl_k,150,116
2@gl_k,154,101,0	warp	#23 Control	2,2,2@gl_k,174,101
2@gl_k,165,101,0	warp	#23 Control2	2,2,2@gl_k,150,111
2@gl_k,150,163,0	warp	#24 Control	2,2,2@gl_k,150,179
2@gl_k,150,167,0	warp	#24 Control2	2,2,2@gl_k,150,149
1@gl_k,69,168,0	warp	#Secret Room Exit	2,2,1@gl_k,48,168

// Floor 1
//============================================================
1@gl_k,149,41,5	script	巴尔蒙德#0	4_M_BARMUND,{
	if (is_party_leader() == false) {
		mes "[巴尔蒙德]";
		mes "你们的队长呢? 我们需要帮助..";
		cutin "gl_barmund2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[巴尔蒙德]";
	mes "你来了？你是来帮助我们的吧？";
	npctalk "巴尔蒙德：你来了？你是来帮助我们的吧？";
	cutin "gl_barmund1",2;
	next;
	select("呃...那个...");
	mes "["+.@player_name$+"]";
	mes "嗯，有人告诉我们来找巴尔蒙德。";
	unittalk .@account_id,.@player_name$ + ": 嗯，有人告诉我们来找巴尔蒙德。";
	next;
	mes "[巴尔蒙德]";
	mes "已经没有多少时间了，我们必须找到海因里希长官，告诉他希梅尔麦兹要来了。";
	npctalk "巴尔蒙德：那别浪费时间了。我们必须告诉海因里希司令官，希梅尔麦兹要来了。";
	cutin "gl_barmund2",2;
	next;
	select("谁是希梅尔麦兹...");
	mes "[" + .@player_name$ + "]";
	mes "希梅尔麦兹？她是谁？";
	unittalk .@account_id,.@player_name$ + ": 希梅尔麦兹？她是谁？";
	next;
	mes "[巴尔蒙德]";
	mes "难道没有人告诉你这些最基本的信息么？";
	npctalk "巴尔蒙德：难道没有人告诉你这些最基本的信息么？";
	cutin "gl_barmund3",2;
	next;
	mes "[巴尔蒙德]";
	mes "她是代表死亡的瓦尔基里。尤米尔的心脏藏在这里，希梅尔麦兹就是为此而来。";
	npctalk "巴尔蒙德：她是代表死亡的瓦尔基里。尤米尔的心脏藏在这里，希梅尔麦兹就是为此而来。";
	cutin "gl_barmund2",2;
	next;
	mes "[巴尔蒙德]";
	mes "为了这个目的，她甚至能摧毁这整座城堡。";
	npctalk "巴尔蒙德：为了这个目的，她甚至能摧毁这整座城堡。";
	next;
	mes "[巴尔蒙德]";
	mes "快点！去告诉海因里希长官这个消息。我要先检查一下周边，希梅尔麦兹很可能已经在这布下了结界！";
	npctalk "巴尔蒙德：快点！去告诉海因里希长官这个消息。我要先检查一下周边，希梅尔麦兹很可能已经在这布下了结界！";
	close2;
	cutin "",255;
	disablenpc instance_npcname("巴尔蒙德#0");
	enablenpc instance_npcname("海因里希#1");
	enablenpc instance_npcname("巴尔蒙德#1");
	end;
}

1@gl_k,145,104,6	script	卡利斯格十字军#1	4_F_KHALITZBURG,{
	mes "[卡利斯格十字军]";
	mes "快要到换班的时间了, 下班要去哪里呢?.";
	close;
}

1@gl_k,154,104,3	script	卡利斯格十字军#2	4_F_KHALITZBURG,{
	mes "[卡利斯格十字军]";
	mes "有什么事情想要告诉我吗?";
	close;
}

1@gl_k,145,99,6	script	纯白骑士#3	4_WHITEKNIGHT,{
	mes "[纯白骑士]";
	mes "你是根巴尔蒙德一起来的? 长官在等你.";
	close;
}

1@gl_k,154,99,3	duplicate(卡利斯格十字军#1)	纯白骑士#4	4_WHITEKNIGHT

1@gl_k,145,94,6	script	卡利斯格十字军#5	4_F_KHALITZBURG,{
	mes "[卡利斯格十字军]";
	mes "我昨天做了一个梦, 我梦到了我的妈妈, 不知道他还好吗?...";
	close;
}

1@gl_k,154,94,3	duplicate(纯白骑士#3)	卡利斯格十字军#6	4_F_KHALITZBURG

1@gl_k,145,89,6	script	纯白骑士#7	4_WHITEKNIGHT,{
	mes "[纯白骑士]";
	mes "请不要在这里乱来.";
	close;
}
1@gl_k,154,89,3	duplicate(纯白骑士#7)	纯白骑士#8	4_WHITEKNIGHT

1@gl_k,145,84,6	script	卡利斯格十字军#9	4_F_KHALITZBURG,{
	mes "[卡利斯格十字军]";
	mes "请遵守秩序.";
	close;
}
1@gl_k,154,84,3	duplicate(卡利斯格十字军#9)	卡利斯格十字军#10	4_F_KHALITZBURG

1@gl_k,145,79,6	script	纯白骑士#11	4_WHITEKNIGHT,{
	mes "[纯白骑士]";
	mes "How do you know 巴尔蒙德? I heard that he's not really a friendly man...";
	close;
}
1@gl_k,154,79,3	duplicate(纯白骑士#11)	纯白骑士#12	4_WHITEKNIGHT

1@gl_k,145,74,6	script	卡利斯格十字军#13	4_F_KHALITZBURG,{
	mes "[卡利斯格十字军]";
	mes "Doesn't this castle seem weird all of a sudden? Something's not quite right.";
	close;
}
1@gl_k,154,74,3	duplicate(卡利斯格十字军#13)	卡利斯格十字军#14	4_F_KHALITZBURG

1@gl_k,145,69,6	script	纯白骑士#15	4_WHITEKNIGHT,{
	mes "[纯白骑士]";
	mes "I do not like to chat during work.";
	close;
}
1@gl_k,154,69,3	duplicate(纯白骑士#15)	纯白骑士#16	4_WHITEKNIGHT

1@gl_k,145,64,6	script	卡利斯格十字军#17	4_F_KHALITZBURG,{
	mes "[卡利斯格十字军]";
	mes "...";
	close;
}
1@gl_k,154,64,3	duplicate(卡利斯格十字军#17)	卡利斯格十字军#18	4_F_KHALITZBURG

1@gl_k,145,59,6	script	纯白骑士#19	4_WHITEKNIGHT,{
	mes "[纯白骑士]";
	mes "What can I do for you?";
	close;
}
1@gl_k,154,59,3	duplicate(纯白骑士#19)	纯白骑士#20	4_WHITEKNIGHT

1@gl_k,145,54,6	script	卡利斯格十字军#21	4_F_KHALITZBURG,{
	mes "[卡利斯格十字军]";
	mes "Is there something you need?";
	close;
}
1@gl_k,154,54,3	duplicate(卡利斯格十字军#21)	卡利斯格十字军#22	4_F_KHALITZBURG

1@gl_k,149,100,6	script	海因里希#1	4_F_KHALITZBURG,{
	if (is_party_leader() == false) {
		mes "[海因里希]";
		mes "你们的队长呢? 我有很重要的事情跟他说.";
		cutin "gl_heinrich2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	cutin "gl_heinrich2",2;
	select("海因里希，这座城堡...");
	mes "[" + .@player_name$ + "]";
	mes "你知道这城中发生了什么吗，海因里希？";
	unittalk .@account_id, .@player_name$ + " : 你知道这城中发生了什么吗，海因里希？";
	next;
	mes "[海因里希]";
	mes "你就是那个和巴尔蒙德一起来的冒险者？";
	npctalk "海因里希：你就是那个和巴尔蒙德一起来的冒险者？";
	next;
	mes "[海因里希]";
	mes "找我有什么事？有什么问题么？";
	npctalk "海因里希：找我有什么事？有什么问题么？";
	next;
	select("尤米尔的心脏，希梅尔麦兹她...");
	mes "["+.@player_name$+"]";
	mes "亡者的巴基力，希梅尔麦兹正在寻找尤米尔的心脏，据说就藏在这座城堡内！";
	unittalk .@account_id, .@player_name$ + " : 亡者的巴基力，希梅尔麦兹正在寻找尤米尔的心脏，据说就藏在这座城堡内！";
	next;
	mes "[海因里希]";
	mes "哈哈。不错，挺好笑的。说实话，你来这有什么事么？";
	npctalk "海因里希：哈哈。不错，挺好笑的。说实话，你来这有什么事么？";
	cutin "gl_heinrich1",2;
	next;
	mes "[巴尔蒙德]";
	mes "我们不是在开玩笑。如果我估计正确的话，希梅尔麦兹应该快来了。";
	npctalk "巴尔蒙德：我们不是在开玩笑。如果我估计正确的话，希梅尔麦兹应该快来了。", instance_npcname("巴尔蒙德#1");
	cutin "gl_barmund2",2;
	next;
	mes "[巴尔蒙德]";
	mes "在她出现之前，我们必须把尤米尔的心脏碎片藏在安全的地方！";
	npctalk "巴尔蒙德：在她出现之前，我们必须把尤米尔的心脏碎片藏在安全的地方！", instance_npcname("巴尔蒙德#1");
	next;
	select("就算你不相信...");
	mes "[" + .@player_name$ + "]";
	mes "就算你不相信这事，这样做总没有坏处。我们已经没有太多时间了！";
	unittalk .@account_id, .@player_name$ + " : 就算你不相信这事，这样做总没有坏处。我们已经没有太多时间了！";
	cutin "",255;
	next;
	mes "[海因里希]";
	mes "谢谢你们的好意，但国王现在不在这。";
	npctalk "海因里希：谢谢你们的好意，但国王现在不在这。";
	cutin "gl_heinrich1",2;
	next;
	mes "[海因里希]";
	mes "我不能随便下令处置如此重要的物品。";
	npctalk "海因里希：我不能随便下令处置如此重要的物品。";
	close2;
	disablenpc instance_npcname("海因里希#1");
	enablenpc instance_npcname("海因里希#2");
	enablenpc instance_npcname("希梅尔麦兹#1");
	donpcevent instance_npcname("#talkinstance1") + "::OnEnable";
	cutin "",255;
	end;
}

1@gl_k,152,97,3	script	巴尔蒙德#1	4_M_BARMUND,{ end; }
1@gl_k,149,97,5	duplicate(巴尔蒙德#1)	海因里希#2	4_F_KHALITZBURG
1@gl_k,149,100,5	duplicate(巴尔蒙德#1)	海因里希#3	4_F_KHALITZBURG
1@gl_k,149,89,1	duplicate(巴尔蒙德#1)	希梅尔麦兹#1	4_F_HIMEL

// Control Timer
//============================================================
1@gl_k,1,1,0	script	#talkinstance1	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	mapannounce 'map_name$[0], "????'s: 啊哈哈哈哈哈~!", bc_map,0xFFFF00,FW_NORMAL,18;
	initnpctimer;
	'npc_himelmez1$ = instance_npcname("希梅尔麦兹#1");
	'npc_varmundt1$ = instance_npcname("巴尔蒙德#1");
	'npc_heinrich2$ = instance_npcname("海因里希#2");
	'npc_heinrich3$ = instance_npcname("海因里希#3");
	end;
OnTimer1500:
	npctalk "海因里希：是谁？！", 'npc_heinrich2$;
	end;
OnTimer4500:
	npctalk "希梅尔麦兹：呵呵~我是不是妨碍你们说话了？客人来了这么久都没发现，这可是很失礼的事情啊...", 'npc_himelmez1$;
	end;
OnTimer10000:
	npctalk "巴尔蒙德：希梅尔麦兹！！什么时候来的？", 'npc_varmundt1$;
	end;
OnTimer17500:
	npctalk "希梅尔麦兹：我是丽莎・卡恩・希梅尔麦兹，人们都叫我不死者的祭司，无头骑士的统治者。", 'npc_himelmez1$;
	end;
OnTimer22000:
	npctalk "海因里希：你的意思是你能操控死者？你看起来是一位优雅的女性，但是我们可不懂得接待淑女...", 'npc_heinrich2$;
	end;
OnTimer28500:
	npctalk "希梅尔麦兹：不要故作镇定了，如果你知道自己的国王现在的情况，估计就不会这么悠闲的在站在这里了。", 'npc_himelmez1$;
	end;
OnTimer36000:
	npctalk "海因里希：你刚刚说什么？！", 'npc_heinrich2$;
	end;
OnTimer41000:
	npctalk "希梅尔麦兹：呵呵呵，这就沉不住气了么？真是个有趣的男人。", 'npc_himelmez1$;
	end;
OnTimer47000:
	npctalk "希梅尔麦兹：虽说你是个没有野心，一直对自己国王尽忠的愚蠢之人，但你的这份纯真我很欣赏。", 'npc_himelmez1$;
	end;
OnTimer54000:
	npctalk "希梅尔麦兹：真希望我们不是在这种情况下相见，太可惜了~", 'npc_himelmez1$;
	end;
OnTimer59000:
	npctalk "海因里希：我们的国王只是受邀前往卢恩米德加兹王国，现在还没有回来而已。", 'npc_heinrich2$;
	end;
OnTimer66500:
	npctalk "海因里希：不过你刚刚这番话是什么意思，国王发生了什么事？", 'npc_heinrich2$;
	end;
OnTimer71500:
	npctalk "希梅尔麦兹：这个嘛...真不凑巧，虽然我很想慢慢说给你听，但我已经没有太多时间了。", 'npc_himelmez1$;
	end;
OnTimer78000:
	npctalk "希梅尔麦兹：我需要继续做我应该做的事了，有什么问题和你自己的部下慢慢聊吧，呵呵呵~", 'npc_himelmez1$;
	end;
OnTimer84500:
	npctalk "海因里希：难道她已经知道尤米尔心脏的位置了？", 'npc_heinrich2$;
	disablenpc 'npc_himelmez1$;
	end;
OnTimer90500:
	npctalk "海因里希：卡利斯格十字军们，纯白骑士们，跟我走...", 'npc_heinrich2$;
	end;
OnTimer92000:
	for (.@i = 1; .@i <= 20; .@i += 4) {
		hideonnpc instance_npcname("卡利斯格十字军#" + .@i);
		hideonnpc instance_npcname("卡利斯格十字军#" + (.@i+1));
		hideonnpc instance_npcname("纯白骑士#" + (.@i+2));
		hideonnpc instance_npcname("纯白骑士#" + (.@i+3));
	}
	hideonnpc instance_npcname("卡利斯格十字军#21");
	hideonnpc instance_npcname("卡利斯格十字军#22");

	.@label$ = instance_npcname("#talkinstance1") + "::OnMyMobDead";
	monster 'map_name$[0],145,59,"堕落的深渊骑士",2470,1,.@label$;// MG_KNIGHT_OF_ABYSS
	monster 'map_name$[0],154,59,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],145,69,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],154,69,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],145,79,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],154,79,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],145,89,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],154,89,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],145,99,"堕落的深渊骑士",2470,1,.@label$;
	monster 'map_name$[0],154,99,"堕落的深渊骑士",2470,1,.@label$;

	monster 'map_name$[0],145,54,"痛苦的卡利斯格",2471,1,.@label$;// G_MG_KHALITZBURG
	unittalk $@mobid[0],"水... 谁能给我些水...";
	monster 'map_name$[0],154,54,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"啊...呃...";
	monster 'map_name$[0],145,64,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"海因里希长官，救命！";
	monster 'map_name$[0],154,64,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我找不到...";
	monster 'map_name$[0],145,74,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"不...不...我不要死...";
	monster 'map_name$[0],154,74,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"啊...呃。";
	monster 'map_name$[0],145,84,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我好渴啊.";
	monster 'map_name$[0],154,84,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我还不想死...";
	monster 'map_name$[0],145,94,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"身体好不舒服，呃！";
	monster 'map_name$[0],154,94,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我是谁...";
	monster 'map_name$[0],145,104,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"啊...我的身体...";
	monster 'map_name$[0],154,104,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我饿了！";
	end;
OnTimer94000:
	npctalk "海因里希：怎么回事！我的部下们...这怎么可能！", 'npc_heinrich2$;
	end;
OnTimer96500:
	npctalk "巴尔蒙德：海因里希，长官！振作点！这不是该忧伤的时候。", 'npc_varmundt1$;
	end;
OnTimer100000:
	npctalk "海因里希：怎么会...", 'npc_heinrich2$;
	end;
OnTimer103000:
	npctalk "海因里希：对不起！请原谅我！", 'npc_heinrich2$;
	end;
OnTimer106000:
	mapannounce 'map_name$[0], "海因里希：怎么会...", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109000:
	mapannounce 'map_name$[0], "海因里希：对不起！请原谅我！", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#1");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#2");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#1");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#2");
	end;
OnTimer110000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#3");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#4");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#5");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#6");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#3");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#4");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#5");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#6");
	end;
OnTimer110500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#7");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#8");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#9");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#10");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#7");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#8");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#9");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#10");
	end;
OnTimer111000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#11");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#12");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#13");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#14");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#11");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#12");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#13");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#14");
	end;
OnTimer111500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#15");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#16");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#17");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#18");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#15");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#16");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#17");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#18");
	end;
OnTimer112000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#19");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("纯白骑士#20");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#21");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字军#22");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#19");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("纯白骑士#20");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#21");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字军#22");
	disablenpc 'npc_heinrich2$;
	enablenpc 'npc_heinrich3$;
	killmonster 'map_name$[0], instance_npcname("#talkinstance1") + "::OnMyMobDead";
	end;
OnTimer112500:
	npctalk "海因里希：我亲手杀了自己的部下...", 'npc_heinrich3$;
	end;
OnTimer115500:
	npctalk "巴尔蒙德：我们要快点阻止席梅尔梅斯！", 'npc_varmundt1$;
	end;
OnTimer118500:
	npctalk "巴尔蒙德：你还是站在我们这边的吧！希望你能帮我们。", 'npc_varmundt1$;
	end;
OnTimer121500:
	npctalk "海因里希：巴尔蒙德是对的，现在不能沉浸在自己的懊悔之中。", 'npc_heinrich3$;
	end;
OnTimer124500:
	npctalk "海因里希：士兵们，现在集合，跟着我。", 'npc_heinrich3$;
	end;
OnTimer127500:
	npctalk "海因里希：希梅尔麦兹会让人类陷入噩梦，然后转化为不死怪物。", 'npc_heinrich3$;
	end;
OnTimer130500:
	npctalk "海因里希：说不定这附近还会有生还者。", 'npc_heinrich3$;
	end;
OnTimer134500:
	npctalk "海因里希：请你帮助我们搜救生还者，并将他们带离这里。", 'npc_heinrich3$;
	end;
OnTimer138500:
	npctalk "海因里希：巴尔蒙德和我现在要去追击希梅尔麦兹。", 'npc_heinrich3$;
	end;
OnTimer143500:
	npctalk "海因里希：走吧，巴尔蒙德，不能让她造成更多伤亡。", 'npc_heinrich3$;
	end;
OnTimer147500:
	disablenpc 'npc_heinrich3$;
	end;
OnTimer148500:
	stopnpctimer;
	donpcevent instance_npcname("#ghmemorialmob01") + "::OnStart";

	disablenpc 'npc_varmundt1$;
	disablenpc instance_npcname("#talkinstance1");
	'npc_himelmez1$ = 'npc_varmundt1$ = 'npc_heinrich2$ = 'npc_heinrich3$ = "";
	end;
OnMyMobDead:
	end;
}

// Sector 1 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob01	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob01");
	enablenpc instance_npcname("#2Control");
	enablenpc instance_npcname("#2Control2");
	mapannounce 'map_name$[0], "9点钟方向的大门已经开启..", bc_map,0xFFFF00;

	// note: x17 MG_GHOUL / x17 MG_ZOMBIE / x17 MG_WRAITH
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	areamonster 'map_name$[0],76,99,87,10,"痛苦的侍从长",2466,14,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"堕落的修道士",2465,17,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"痛苦的侍从长",2466,3,.@label$;
	areamonster 'map_name$[0],32,75,51,58,"堕落的管家",2464,5,.@label$;
	areamonster 'map_name$[0],45,84,6,137,"堕落的管家",2464,12,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	.@mob_dead_num = 51 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 35) {
		mapannounce 'map_name$[0], "看起来希梅尔麦兹吸取了这些死者的灵魂，现在找找看有没有生还者吧。", bc_map,0xFFFFFF;
		killmonster 'map_name$[0], .@label$;
		enablenpc instance_npcname("候补的服事少年#1");
		disablenpc instance_npcname("#ghmemorialmob01");
	}
	end;
}

1@gl_k,17,51,3	script	候补的服事少年#1	4_M_KID1,{
	if (is_party_leader() == false) {
		mes "[候补的服事少年]";
		mes "Save me, save me, please...";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[候补的服事少年]";
	mes "救命！救命！！！";
	next;
	select("嘿，冷静点！这还有其他生还者么？");
	mes "["+.@player_name$+"]";
	mes "嘿，冷静点！还有其他人么？";
	unittalk .@account_id, .@player_name$ + " : 嘿，冷静点！还有其他人么？";
	next;
	mes "[候补的服事少年]";
	mes "侍从们...僧侣们...他们都变成了怪物，我只能躲起来。";
	npctalk "候补的服事少年：侍从们...僧侣们...他们都变成了怪物，我只能躲起来。";
	next;
	mes "[候补的服事少年]";
	mes "我只能在这躲着...一点都不敢动...";
	npctalk "候补的服事少年：我只能在这躲着...一点都不敢动...";
	next;
	select("冷静！");
	mes "["+.@player_name$+"]";
	mes "冷静！现在往东走，从中央大厅出去后就安全了！";
	unittalk .@account_id, .@player_name$ + " : 冷静！现在往东走，从中央大厅出去后就安全了！";
	next;
	mes "[候补的服事少年]";
	mes "东边？一个人么？现在就走？";
	npctalk "候补的服事少年：东边？一个人么？现在就走？";
	next;
	select("我可以带你过去。");
	mes "["+.@player_name$+"]";
	mes "我带你过去吧。如果你害怕的话，闭上眼睛跟着走。";
	unittalk .@account_id, .@player_name$ + " : 我带你过去吧。如果你害怕的话，闭上眼睛跟着走。";
	next;
	mes "[候补的服事少年]";
	mes "好的，我会努力。";
	npctalk "候补的服事少年：好的，我会努力。";
	disablenpc instance_npcname("候补的服事少年#1");
	donpcevent instance_npcname("#ghmemorialmob02") + "::OnStart";
	close;
}

// Sector 2 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob02	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob02");
	enablenpc instance_npcname("#3Control");
	enablenpc instance_npcname("#3Control2");
	for ( .@i = 1; .@i <= 26; .@i++ )
		enablenpc instance_npcname("已经死掉的人#" + .@i);
	mapannounce 'map_name$[0], "3点方向的传送点已开放，现在可以前往区域3了", bc_map,0xFFFF00;

	// note: x34 MG_ZOMBIE / x35 MG_GHOUL
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	areamonster 'map_name$[0],236,12,288,52,"愤怒的敲击者",2466,18,.@label$;// MG_GHOUL
	areamonster 'map_name$[0],236,12,288,52,"腐烂的铁匠",2464,17,.@label$;// MG_ZOMBIE
	areamonster 'map_name$[0],242,71,286,145,"愤怒的敲击者",2466,17,.@label$;
	areamonster 'map_name$[0],242,71,286,145,"腐烂的铁匠",2464,17,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	.@mob_dead_num = 69 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 56) {
		mapannounce 'map_name$[0], "看起来希梅尔麦兹吸取了这些死者的灵魂，现在找找看有没有生还者吧。", bc_map,0xFFFFFF;
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("精炼工匠的母亲#1");
		disablenpc instance_npcname("#ghmemorialmob02");
	}
	end;
}

1@gl_k,291,145,3	script	精炼工匠的母亲#1	4_F_JOB_BLACKSMITH,{
	if (is_party_leader() == false) {
		mes "[精炼工匠的母亲]";
		mes "有人么？这里需要帮助...";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[精炼工匠的母亲]";
	mes "呀！！死吧！！！";
	npctalk "精炼工匠的母亲：呀！！死吧！！！";
	specialeffect EF_CRASHEARTH;
	next;
	select("别担心！");
	mes "["+.@player_name$+"]";
	mes "不要紧张！女士，你一个人么？还有其他生还者么？";
	unittalk .@account_id, .@player_name$ + " : 不要紧张！女士，你一个人么？还有其他生还者么？";
	next;
	mes "[精炼工匠的母亲]";
	mes "这里只有我一个了。";
	npctalk "精炼工匠的母亲：这里只有我一个了。";
	next;
	select("这里现在非常危险。");
	mes "["+.@player_name$+"]";
	mes "这个地方非常危险。你知道中央通道吧？我看你应该还能走过去...从那出去就安全了。";
	unittalk .@account_id, .@player_name$ + " : 这个地方非常危险。你知道中央通道吧？我看你应该还能走过去...从那出去就安全了。";
	next;
	mes "[精炼工匠的母亲]";
	mes "好的，我还能动，为了我的孩子我一定要回去。";
	npctalk "精炼工匠的母亲：好的，我还能动，为了我的孩子我一定要回去。";
	next;
	select("一定要活下去...");
	mes "["+.@player_name$+"]";
	mes "我相信你一定能坚持下去的。但我现在不能帮你了。";
	unittalk .@account_id, .@player_name$ + " : 我相信你一定能坚持下去的。但我现在不能帮你了。";
	next;
	mes "[精炼工匠的母亲]";
	mes "没关系，谢谢你来救我，我能一个人过去，祝你好运！";
	npctalk "精炼工匠的母亲：没关系，谢谢你来救我，我能一个人过去，祝你好运！";
	disablenpc instance_npcname("精炼工匠的母亲#1");
	donpcevent instance_npcname("#ghmemorialmob03") + "::OnEnable";
	close;
}

// Tramp Mobs
//============================================================
1@gl_k,221,82,3	script	已经死掉的人#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"蛆虫",2467,.@mobs;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,233,123,4	duplicate(已经死掉的人#1)	已经死掉的人#2	4_M_DIEMAN,5,5
1@gl_k,258,150,2	duplicate(已经死掉的人#1)	已经死掉的人#3	4_M_DIEMAN,5,5
1@gl_k,255,157,5	duplicate(已经死掉的人#1)	已经死掉的人#4	4_M_DIEMAN,5,5
1@gl_k,280,167,4	duplicate(已经死掉的人#1)	已经死掉的人#5	4_M_DIEMAN,5,5
1@gl_k,293,161,2	duplicate(已经死掉的人#1)	已经死掉的人#6	4_M_DIEMAN,5,5
1@gl_k,249,101,3	duplicate(已经死掉的人#1)	已经死掉的人#7	4_M_DIEMAN,5,5
1@gl_k,241,86,6	duplicate(已经死掉的人#1)	已经死掉的人#8	4_M_DIEMAN,5,5
1@gl_k,246,62,0	duplicate(已经死掉的人#1)	已经死掉的人#9	4_M_DIEMAN,5,5
1@gl_k,240,43,7	duplicate(已经死掉的人#1)	已经死掉的人#10	4_M_DIEMAN,5,5
1@gl_k,271,19,1	duplicate(已经死掉的人#1)	已经死掉的人#11	4_M_DIEMAN,5,5
1@gl_k,282,48,5	duplicate(已经死掉的人#1)	已经死掉的人#12	4_M_DIEMAN,5,5
1@gl_k,285,81,7	duplicate(已经死掉的人#1)	已经死掉的人#13	4_M_DIEMAN,5,5
1@gl_k,276,106,0	duplicate(已经死掉的人#1)	已经死掉的人#14	4_M_DIEMAN,5,5
1@gl_k,261,164,0	duplicate(已经死掉的人#1)	已经死掉的人#15	4_M_DIEMAN,5,5
1@gl_k,269,173,0	duplicate(已经死掉的人#1)	已经死掉的人#16	4_M_DIEMAN,5,5
1@gl_k,252,120,0	duplicate(已经死掉的人#1)	已经死掉的人#17	4_M_DIEMAN,5,5
1@gl_k,213,63,0	duplicate(已经死掉的人#1)	已经死掉的人#18	4_M_DIEMAN,5,5
1@gl_k,222,39,0	duplicate(已经死掉的人#1)	已经死掉的人#19	4_M_DIEMAN,5,5
1@gl_k,214,27,0	duplicate(已经死掉的人#1)	已经死掉的人#20	4_M_DIEMAN,5,5
1@gl_k,223,17,1	duplicate(已经死掉的人#1)	已经死掉的人#21	4_M_DIEMAN,5,5
1@gl_k,230,50,2	duplicate(已经死掉的人#1)	已经死掉的人#22	4_M_DIEMAN,5,5
1@gl_k,235,16,3	duplicate(已经死掉的人#1)	已经死掉的人#23	4_M_DIEMAN,5,5
1@gl_k,226,96,4	duplicate(已经死掉的人#1)	已经死掉的人#24	4_M_DIEMAN,5,5
1@gl_k,222,119,5	duplicate(已经死掉的人#1)	已经死掉的人#25	4_M_DIEMAN,5,5
1@gl_k,251,20,6	duplicate(已经死掉的人#1)	已经死掉的人#26	4_M_DIEMAN,5,5

// Sector 3 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob03	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob03");
	enablenpc instance_npcname("#4Control");
	enablenpc instance_npcname("#4Control2");
	mapannounce 'map_name$[0], "12点方向的传送门(区域3内)已开放。", bc_map,0xFFFF00;

	// note: x50 MG_RAYDRIC / x50 MG_RAYDRIC_ARCHER
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	areamonster 'map_name$[0],16,186,43,280,"愤怒的弓箭手",2469,25,.@label$;
	areamonster 'map_name$[0],44,191,89,225,"饥饿的近卫兵",2468,25,.@label$;
	areamonster 'map_name$[0],115,215,188,273,"腐败的近卫兵",2468,20,.@label$;
	areamonster 'map_name$[0],108,232,193,281,"腐败的近卫兵",2468,5,.@label$;
	areamonster 'map_name$[0],158,236,230,250,"徘徊的弓箭手",2469,12,.@label$;
	areamonster 'map_name$[0],65,231,155,256,"徘徊的弓箭手",2469,13,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	.@mob_dead_num = 100 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 85) {
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("#Mimelon");
		enablenpc instance_npcname("希梅尔麦兹#2");
		enablenpc instance_npcname("巴尔蒙德#2");
		enablenpc instance_npcname("海因里希#4");
		disablenpc instance_npcname("#ghmemorialmob03");
		mapannounce 'map_name$[0], "希梅尔麦兹：不错嘛，我还以为你们会死在我的不死军团手里~", bc_map,0xFFFFFF;
		initnpctimer;
	}
	end;
OnTimer5000:
	mapannounce 'map_name$[0], "希梅尔麦兹: 我在北面等你, 本来是想看看你有没有运气走到这里.", bc_map,0xFFFFFF;
	stopnpctimer;
	end;
}

// 1st MVP
//============================================================
1@gl_k,144,258,6	script	海因里希#4	4_F_KHALITZBURG,{
	mes "[海因里希]";
	mes "希梅尔麦兹...你杀了我这么多部下，我永远不会忘记这些。";
	cutin "gl_heinrich1",2;
	close3;
}

// note: never hidden
1@gl_k,156,258,3	script	巴尔蒙德#2	4_M_BARMUND,{
	cutin "gl_barmund1",2;
	mes "[巴尔蒙德]";
	if (checkquest(12318,HUNTING) != 2) {
		mes "我们一定要想办法阻止这一切！";
		close3;
	}
	erasequest 12318;
	if (isbegin_quest(12319) == 0)
		setquest 12319;// Amdarais Hunt
	if (isbegin_quest(12320) == 0) {
		setquest 12320;// Time Traveler
		completequest 12320;
		getexp 250000,250000;
	}
	mes "这些东西带有一种非比寻常的魔力，如果能够利用的话，肯定会对你有很大的帮助。";
	getitem 6607,1;// Temporal_Crystal
	getitem 6608,1;// Coagulated_Spell
	close3;
}

1@gl_k,150,257,3	script	希梅尔麦兹#2	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[希梅尔麦兹]";
	mes "别这样盯着我看，他们这不是得到了永久的安息么...";
	close3;
}

1@gl_k,150,257,0	script	#Mimelon	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Mimelon");
	initnpctimer;
	'npc_himelmez2$ = instance_npcname("希梅尔麦兹#2");
	'npc_heinrich4$ = instance_npcname("海因里希#4");
	end;
OnTimer3000:
	npctalk "希梅尔麦兹：能跟着我到这，运气挺不错嘛。", 'npc_himelmez2$;
	end;
OnTimer6000:
	npctalk "希梅尔麦兹：不过也就到此为止了。", 'npc_himelmez2$;
	end;
OnTimer9000:
	npctalk "希梅尔麦兹：现在，你们全都要死。", 'npc_himelmez2$;
	end;
OnTimer12000:
	npctalk "海因里希：希梅尔麦兹！你别想再进一步。", 'npc_heinrich4$;
	end;
OnTimer15000:
	npctalk "希梅尔麦兹：哈哈哈，你们觉得我会一个人过来么？", 'npc_himelmez2$;
	end;
OnTimer18000:
	npctalk "巴尔蒙德：海因里希长官！看起来有未知的有强力生物在靠近！", instance_npcname("巴尔蒙德#2");
	end;
OnTimer21000:
	npctalk "海因里希：这...这是什么？！", 'npc_heinrich4$;
	end;
OnTimer24000:
	npctalk "希梅尔麦兹：给你们看看我的新玩具，你们就好好一起玩玩吧。", 'npc_himelmez2$;
	end;
OnTimer27000:
	npctalk "希梅尔麦兹：另外~，很期待再次见到你哦，海因里希。", 'npc_himelmez2$;
	end;
OnTimer28000:
	disablenpc 'npc_himelmez2$;
	end;
OnTimer31000:
	npctalk "海因里希：巴尔蒙德！帮一下冒险者们，我先去追希梅尔麦兹了！", 'npc_heinrich4$;
	end;
OnTimer32000:
	disablenpc 'npc_heinrich4$;
	end;
OnTimer35000:
	mapannounce 'map_name$[0], "回声来自于骑士神殿的深处。", bc_map,0xFFFFFF;
	end;
OnTimer38000:
	donpcevent instance_npcname("#ghmemorialmob04") + "::OnStart";
	stopnpctimer;
	'npc_himelmez2$ = 'npc_heinrich4$ = "";
	end;
}

1@gl_k,1,1,0	script	#ghmemorialmob04	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob04");
	monster 'map_name$[0],150,259,"堕落之根",2475,1, instance_npcname("#ghmemorialmob04") + "::OnMyMobDead";// MG_CORRUPTION_ROOT
	unittalk $@mobid[0],"呃~~啊~~~";
	end;
OnMyMobDead:
	if (mobcount('map_name$[0], instance_npcname("#ghmemorialmob04") + "::OnMyMobDead") < 1) {
		mapannounce 'map_name$[0], "12点方向的传送门已打开，现在可以从中央大厅到二层了", bc_map,0xFFFF00;
		npctalk "巴尔蒙德：我从怪物身上找到了一些奇怪的东西，有兴趣的人可以来找我来拿。", instance_npcname("巴尔蒙德#2");

		enablenpc instance_npcname("#2F Entrance");
		enablenpc instance_npcname("#1 Control");
		enablenpc instance_npcname("#22 Control");
		enablenpc instance_npcname("#22 Control2");
		enablenpc instance_npcname("#Servanton");
		enablenpc instance_npcname("海因里希#21");
		enablenpc instance_npcname("巴尔蒙德#21");
		donpcevent instance_npcname("#ghmemorialmob05") + "::OnStart";
		disablenpc instance_npcname("#ghmemorialmob04");
	}
	end;
}

// Floor 2
//============================================================
2@gl_k,148,67,1	script	海因里希#21	4_F_KHALITZBURG,{
	mes "[海因里希]";
	mes "希梅尔麦兹的结界，";
	mes "这里到处都是...";
	cutin "gl_heinrich1",2;
	close3;
}

2@gl_k,151,71,7	script	巴尔蒙德#21	4_M_BARMUND,{
	mes "[巴尔蒙德]";
	mes "我怎么又在做这事？这好像并不是第一次，我在梦中重复见过这个场景。这个感觉...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,150,66,0	script	#Servanton_effect	HIDDEN_WARP_NPC,{
	end;
OnEffect:
	specialeffect EF_LORD;
	end;
}

// note: this part can be skipped
2@gl_k,150,66,0	script	#Servanton	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Servanton");
	initnpctimer;
	'npc_heinrich$ = instance_npcname("海因里希#21");
	'npc_varmundt$ = instance_npcname("巴尔蒙德#21");
	end;
OnTimer3000:
	npctalk "海因里希：这座城堡里还从未有过这些东西！", 'npc_heinrich$;
	end;
OnTimer6000:
	npctalk "巴尔蒙德：希梅尔麦兹的结界完全阻绝了我们的去路。", 'npc_varmundt$;
	end;
OnTimer9000:
	npctalk "海因里希：普通的武器根本无法破坏它们，我试过了。", 'npc_heinrich$;
	end;
OnTimer12000:
	npctalk "巴尔蒙德：请让开一下，我来尝试破坏它们。", 'npc_varmundt$;
	end;
OnTimer15000:
	specialeffect EF_LORD,AREA, instance_npcname("#Servanton_effect");
	disablenpc instance_npcname("#Servanton_effect");
	end;
OnTimer18000:
	npctalk "巴尔蒙德：似乎有一边的结界被破坏了。", 'npc_varmundt$;
	end;
OnTimer21000:
	npctalk "巴尔蒙德：我怎么对这个结界有些印象", 'npc_varmundt$;
	end;
OnTimer24000:
	npctalk "巴尔蒙德：这些结界并不是依靠魔法刻印存在的。", 'npc_varmundt$;
	end;
OnTimer27000:
	npctalk "巴尔蒙德：希梅尔麦兹使用的是魔石，这些魔石提供了维持结界所需的能量。", 'npc_varmundt$;
	end;
OnTimer30000:
	npctalk "巴尔蒙德：我们需要找到持有魔石的不死怪物，消灭它们并摧毁魔石。", 'npc_varmundt$;
	end;
OnTimer33000:
	npctalk "巴尔蒙德：但我们无法感受到魔石的确切位置，所以需要尽量多的消灭不死怪物。", 'npc_varmundt$;
	end;
OnTimer36000:
	npctalk "海因里希：又要亲手...真是无法原谅！", 'npc_heinrich$;
	end;
OnTimer39000:
	npctalk "海因里希：牺牲的人实在太多了，还不知道接下来会有多少...", 'npc_heinrich$;
	end;
OnTimer42000:
	npctalk "巴尔蒙德：长官...", 'npc_varmundt$;
	end;
OnTimer45000:
	npctalk "巴尔蒙德：长官，你还不能在这个时候忧伤。", 'npc_varmundt$;
	end;
OnTimer48000:
	npctalk "巴尔蒙德：而且他们已经成为怪物了，不是我们以前的同伴了。", 'npc_varmundt$;
	end;
OnTimer51000:
	npctalk "巴尔蒙德：他们不会再变回来了。", 'npc_varmundt$;
	end;
OnTimer54000:
	npctalk "海因里希: ...", 'npc_heinrich$;
	end;
OnTimer57000:
	npctalk "巴尔蒙德：走吧，继续前进。", 'npc_varmundt$;
	end;
OnTimer60000:
	npctalk "巴尔蒙德：冒险者们，谢谢你们一路帮到这，接下来也希望得到你们的帮助。", 'npc_varmundt$;
	end;
OnTimer63000:
	npctalk "巴尔蒙德：前面将是更加艰难的战斗，最好先准备准备。", 'npc_varmundt$;
	end;
OnTimer66000:
	npctalk "巴尔蒙德：好了，我们出发吧，海因里希长官。", 'npc_varmundt$;
	end;
OnTimer69000:
	mapannounce 'map_name$[1], "通往西边区域的大门已在中央走廊打开.", bc_map,0xFFFF00;
	disablenpc 'npc_varmundt$;
	disablenpc 'npc_heinrich$;
	stopnpctimer;
	'npc_varmundt$ = 'npc_heinrich$ = "";
	end;
}

// Spots
// Note: timer is the only condition for them to respawn
2@gl_k,118,141,0	script	#ogh_2-1	HIDDEN_WARP_NPC,20,20,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@hidden_name$ = strnpcinfo(2);
	.@event_type = atoi( charat(.@hidden_name$,4) );
	if (.@event_type == 2)
		.@label$ = instance_npcname("#ghmemorialmob05") + "::OnMyMobDead";
	else
		.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	getmapxy .@map$,.@x,.@y, BL_NPC;
	monster .@map$,.@x,.@y, "腐败的近卫兵", 2468,1, .@label$;
	monster .@map$,.@x,.@y, "死灵弓箭手", 2469,1, .@label$;
	monster .@map$,.@x,.@y, "堕落的深渊骑士", 2470,1, .@label$;
	monster .@map$,.@x,.@y, "痛苦的卡利斯格", 2471,1, .@label$;
	monster .@map$,.@x,.@y, "血腥骑士", 2472,1, .@label$;
	initnpctimer;
	end;
OnTimer30000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
OnStop:
	disablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,128,81,0	duplicate(#ogh_2-1)	#ogh_2-2	HIDDEN_WARP_NPC,20,20
2@gl_k,131,54,0	duplicate(#ogh_2-1)	#ogh_2-3	HIDDEN_WARP_NPC,20,20
2@gl_k,89,48,0	duplicate(#ogh_2-1)	#ogh_2-4	HIDDEN_WARP_NPC,20,20
2@gl_k,64,117,0	duplicate(#ogh_2-1)	#ogh_2-5	HIDDEN_WARP_NPC,20,20
2@gl_k,62,82,0	duplicate(#ogh_2-1)	#ogh_2-6	HIDDEN_WARP_NPC,20,20
2@gl_k,38,138,0	duplicate(#ogh_2-1)	#ogh_2-7	HIDDEN_WARP_NPC,20,20

2@gl_k,171,120,0	duplicate(#ogh_2-1)	#ogh_3-1	HIDDEN_WARP_NPC,20,20
2@gl_k,232,133,0	duplicate(#ogh_2-1)	#ogh_3-2	HIDDEN_WARP_NPC,20,20
2@gl_k,256,149,0	duplicate(#ogh_2-1)	#ogh_3-3	HIDDEN_WARP_NPC,20,20
2@gl_k,212,106,0	duplicate(#ogh_2-1)	#ogh_3-4	HIDDEN_WARP_NPC,20,20
2@gl_k,243,73,0	duplicate(#ogh_2-1)	#ogh_3-5	HIDDEN_WARP_NPC,20,20
2@gl_k,229,26,0	duplicate(#ogh_2-1)	#ogh_3-6	HIDDEN_WARP_NPC,20,20
2@gl_k,181,34,0	duplicate(#ogh_2-1)	#ogh_3-7	HIDDEN_WARP_NPC,20,20

// Commander 1
2@gl_k,1,1,0	script	#ghmemorialmob05	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_2-" + .@i);
	end;

OnMyMobDead:
	if (rand(50) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "邪恶的气息在此地愈发凝重。", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob05") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 41,146, 58,44, 122,148, 131,64;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "毁灭的第1骑士团长",2473,1, instance_npcname("#ghmemorialmob05") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "通往东边区域的大门已在中央走廊打开.", bc_map,0xFFFF00;
	enablenpc instance_npcname("#23 Control");
	enablenpc instance_npcname("#23 Control2");
	donpcevent instance_npcname("#ghmemorialmob06") + "::OnStart";

	disablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_2-" + .@i) + "::OnStop";
	end;
}

// Commander 2
2@gl_k,1,1,0	script	#ghmemorialmob06	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_3-" + .@i);

	// Fix spawn
	.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	monster 'map_name$[1],252, 75, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],253, 76, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],247, 77, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],248, 80, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],236,100, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],240,100, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],242,100, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],244,100, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],199,111, "腐败的近卫兵",2468,1, .@label$;
	monster 'map_name$[1],181,107, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],177,110, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],236, 27, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],242, 27, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],252, 26, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],172,130, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],171,127, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],173,127, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],177, 69, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],186, 66, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],189, 67, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],190, 68, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],257,157, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],246,159, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],237,158, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],206,159, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],172,120, "堕落的深渊骑士",2470,1, .@label$;
	monster 'map_name$[1],226, 30, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],226, 38, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],228, 29, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],226, 63, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],268,137, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],263,138, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],259,138, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],259,138, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],187,121, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],265, 52, "血腥骑士",2472,1, .@label$;
	monster 'map_name$[1],265, 55, "血腥骑士",2472,1, .@label$; 
	monster 'map_name$[1],264, 79, "血腥骑士",2472,1, .@label$; 
	monster 'map_name$[1],225, 92, "血腥骑士",2472,1, .@label$; 
	monster 'map_name$[1],237, 92, "血腥骑士",2472,1, .@label$; 
	monster 'map_name$[1],249, 92, "血腥骑士",2472,1, .@label$; 
	monster 'map_name$[1],256, 93, "血腥骑士",2472,1, .@label$; 
	monster 'map_name$[1],264,100, "血腥骑士",2472,1, .@label$;
	monster 'map_name$[1],181,120, "血腥骑士",2472,1, .@label$;
	end;

OnMyMobDead:
	if (rand(50) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "邪恶的气息开始在此处蔓延。", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob06") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 166,119, 211,45, 227,139, 245,74;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "毁灭的第2骑士团长",2474,1, instance_npcname("#ghmemorialmob06") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "中央走廊尽头的大门已经打开。", bc_map,0xFFFF00;
	enablenpc instance_npcname("#24 Control");
	enablenpc instance_npcname("#24 Control2");
	enablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		enablenpc instance_npcname("腐烂的尸体#" + .@i);

	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_3-" + .@i) + "::OnStop";

	// Hidden mobs
	setarray .@coord[0],
		 37,265, 57,265, 77,265, 97,265, 117,265, 188,264, 208,264,
		 50,172, 70,172, 90,172, 110,172, 170,172, 210,172, 230,172,
		 88,214, 108,214, 128,214, 180,219, 200,219, 220,219, 240,219;
	.@size = getarraysize(.@coord);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		monster 'map_name$[1], .@coord[.@i], .@coord[.@i+1], "Flame of destruction",2337,1;// HIDDEN_MOB
	end;
}

// Amdarais Room Entrance
2@gl_k,150,180,0	script	#Geron	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Geron");
	mapannounce 'map_name$[1], "???：不要过来！到处都是陷阱...啊！！！！", bc_map,0xFF7777,FW_NORMAL,15;
	enablenpc instance_npcname("希梅尔麦兹#22");
	enablenpc instance_npcname("巴尔蒙德#23");
	enablenpc instance_npcname("海因里希#23");
	enablenpc instance_npcname("格哈德#23");
	end;
}

2@gl_k,143,260,4	script	腐烂的尸体#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Maggot",2467,.@mobs;
	initnpctimer;
	end;
OnTimer45000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,145,236,4	duplicate(腐烂的尸体#1)	腐烂的尸体#2	4_M_DIEMAN,5,5
2@gl_k,141,222,2	duplicate(腐烂的尸体#1)	腐烂的尸体#3	4_M_DIEMAN,5,5
2@gl_k,147,203,5	duplicate(腐烂的尸体#1)	腐烂的尸体#4	4_M_DIEMAN,5,5
2@gl_k,167,225,4	duplicate(腐烂的尸体#1)	腐烂的尸体#5	4_M_DIEMAN,5,5
2@gl_k,172,233,2	duplicate(腐烂的尸体#1)	腐烂的尸体#6	4_M_DIEMAN,5,5
2@gl_k,176,244,3	duplicate(腐烂的尸体#1)	腐烂的尸体#7	4_M_DIEMAN,5,5
2@gl_k,184,248,6	duplicate(腐烂的尸体#1)	腐烂的尸体#8	4_M_DIEMAN,5,5
2@gl_k,193,228,0	duplicate(腐烂的尸体#1)	腐烂的尸体#9	4_M_DIEMAN,5,5
2@gl_k,206,250,7	duplicate(腐烂的尸体#1)	腐烂的尸体#10	4_M_DIEMAN,5,5
2@gl_k,130,249,1	duplicate(腐烂的尸体#1)	腐烂的尸体#11	4_M_DIEMAN,5,5
2@gl_k,122,236,5	duplicate(腐烂的尸体#1)	腐烂的尸体#12	4_M_DIEMAN,5,5
2@gl_k,130,228,7	duplicate(腐烂的尸体#1)	腐烂的尸体#13	4_M_DIEMAN,5,5
2@gl_k,106,226,0	duplicate(腐烂的尸体#1)	腐烂的尸体#14	4_M_DIEMAN,5,5
2@gl_k,104,245,0	duplicate(腐烂的尸体#1)	腐烂的尸体#15	4_M_DIEMAN,5,5
2@gl_k,131,187,0	duplicate(腐烂的尸体#1)	腐烂的尸体#16	4_M_DIEMAN,5,5
2@gl_k,121,197,0	duplicate(腐烂的尸体#1)	腐烂的尸体#17	4_M_DIEMAN,5,5
2@gl_k,107,194,0	duplicate(腐烂的尸体#1)	腐烂的尸体#18	4_M_DIEMAN,5,5
2@gl_k,92,187,0	duplicate(腐烂的尸体#1)	腐烂的尸体#19	4_M_DIEMAN,5,5
2@gl_k,153,214,3	duplicate(腐烂的尸体#1)	腐烂的尸体#20	4_M_DIEMAN,5,5
2@gl_k,155,195,4	duplicate(腐烂的尸体#1)	腐烂的尸体#21	4_M_DIEMAN,5,5
2@gl_k,154,188,2	duplicate(腐烂的尸体#1)	腐烂的尸体#22	4_M_DIEMAN,5,5
2@gl_k,143,195,5	duplicate(腐烂的尸体#1)	腐烂的尸体#23	4_M_DIEMAN,5,5
2@gl_k,132,214,4	duplicate(腐烂的尸体#1)	腐烂的尸体#24	4_M_DIEMAN,5,5
2@gl_k,125,208,2	duplicate(腐烂的尸体#1)	腐烂的尸体#25	4_M_DIEMAN,5,5
2@gl_k,114,210,3	duplicate(腐烂的尸体#1)	腐烂的尸体#26	4_M_DIEMAN,5,5
2@gl_k,137,182,6	duplicate(腐烂的尸体#1)	腐烂的尸体#27	4_M_DIEMAN,5,5
2@gl_k,138,246,0	duplicate(腐烂的尸体#1)	腐烂的尸体#28	4_M_DIEMAN,5,5
2@gl_k,132,260,7	duplicate(腐烂的尸体#1)	腐烂的尸体#29	4_M_DIEMAN,5,5
2@gl_k,128,251,1	duplicate(腐烂的尸体#1)	腐烂的尸体#30	4_M_DIEMAN,5,5
2@gl_k,179,260,3	duplicate(腐烂的尸体#1)	腐烂的尸体#31	4_M_DIEMAN,5,5
2@gl_k,170,261,4	duplicate(腐烂的尸体#1)	腐烂的尸体#32	4_M_DIEMAN,5,5
2@gl_k,177,219,2	duplicate(腐烂的尸体#1)	腐烂的尸体#33	4_M_DIEMAN,5,5
2@gl_k,190,214,5	duplicate(腐烂的尸体#1)	腐烂的尸体#34	4_M_DIEMAN,5,5
2@gl_k,201,214,4	duplicate(腐烂的尸体#1)	腐烂的尸体#35	4_M_DIEMAN,5,5

// Amdarais Spawn
2@gl_k,153,250,8	script	海因里希#23	4_F_KHALITZBURG,{
	cutin "gl_heinrich1",2;
	mes "[海因里希]";
	mes "你在这里做什么? 希梅尔麦兹!";
	close3;
}

2@gl_k,162,250,1	script	巴尔蒙德#23	4_M_BARMUND,{
	mes "[巴尔蒙德]";
	mes "不能让他们跑掉...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,158,255,1	script	格哈德#23	4_LEVITATEMAN,{
	mes "[格哈德]";
	mes "呃... 啊! 快带着长官离开这里.";
	close;
}

2@gl_k,158,252,1	script	希梅尔麦兹#22	4_F_HIMEL,3,3,{
	cutin "gl_himel2",2;
	mes "[希梅尔麦兹]";
	mes "这太令人惊奇了，你一直都让它留在这里。我可以说声谢谢吗？";
	close3;

OnTouch:
	end;
OnTouch_:
	disablenpc instance_npcname("希梅尔麦兹#22");
	enablenpc instance_npcname("希梅尔麦兹#23");
	initnpctimer;
	'npc_himelmez$ = instance_npcname("希梅尔麦兹#23");
	'npc_gerhalt$ = instance_npcname("格哈德#23");
	'npc_heinrich$ = instance_npcname("海因里希#23");
	'npc_varmundt$ = instance_npcname("巴尔蒙德#23");
	end;
OnTimer3000:
	npctalk "希梅尔麦兹：很好~我还以为你们没办法来到这里...", 'npc_himelmez$;
	end;
OnTimer6000:
	npctalk "格哈德：混帐！快跑！我快要坚持不住了！", 'npc_gerhalt$;
	end;
OnTimer9000:
	npctalk "海因里希: 格哈德!", 'npc_heinrich$;
	end;
OnTimer15000:
	npctalk "海因里希：你想对我的部下做什么，希梅尔麦兹？！", 'npc_heinrich$;
	end;
OnTimer18000:
	npctalk "希梅尔麦兹：呵呵，我已经找到尤米尔心脏碎片了，海因里希。", 'npc_himelmez$;
	end;
OnTimer21000:
	npctalk "希梅尔麦兹：没有你们干扰的话，本来不需要花这么久时间。", 'npc_himelmez$;
	end;
OnTimer24000:
	npctalk "格哈德：长官...快走，你必须活下来...呃。.", 'npc_gerhalt$;
	end;
OnTimer27000:
	npctalk "希梅尔麦兹：你说怎么样？就假装这是一场瘟疫，席卷了国王和他的人民", 'npc_himelmez$;
	end;
OnTimer30000:
	npctalk "海因里希：希梅尔麦兹！有必要做到这一步么？！", 'npc_heinrich$;
	end;
OnTimer33000:
	npctalk "海因里希：我不会让你再伤害我的部下！", 'npc_heinrich$;
	end;
OnTimer36000:
	npctalk "希梅尔麦兹：你真的这么天真？", 'npc_himelmez$;
	end;
OnTimer39000:
	npctalk "希梅尔麦兹：这一次就和你的部下在地狱相见吧，海因里希。", 'npc_himelmez$;
	end;
OnTimer42000:
	npctalk "希梅尔麦兹：看来你有点认不清形势，这里没人能够阻止我。", 'npc_himelmez$;
	end;
OnTimer45000:
	npctalk "希梅尔麦兹：心情不错，这次就让你们见识我的新宠物吧，暗达莱斯。", 'npc_himelmez$;
	end;
OnTimer48000:
	npctalk "海因里希：我一定要让你付出代价！", 'npc_heinrich$;
	end;
OnTimer51000:
	npctalk "希梅尔麦兹：哦~好可怕。", 'npc_himelmez$;
	end;
OnTimer54000:
	npctalk "希梅尔麦兹：哈哈，这次我玩得很开心，下次再见咯。", 'npc_himelmez$;
	end;
OnTimer57000:
	npctalk "希梅尔麦兹：还会再见的，不是么？", 'npc_himelmez$;
	end;
OnTimer60000:
	specialeffect EF_BARRIER, AREA, 'npc_gerhalt$;
	end;
OnTimer63000:
	disablenpc 'npc_himelmez$;
	end;
OnTimer65000:
	npctalk "格哈德：你可以控制我的身体，但你无法夺走我的灵魂，希梅尔麦兹！", 'npc_gerhalt$;
	end;
OnTimer66000:
	specialeffect EF_CHAINCOMBO, AREA, 'npc_gerhalt$;
	end;
OnTimer67000:
	npctalk "海因里希：希梅尔麦兹！！", 'npc_heinrich$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	end;
OnTimer70000:
	specialeffect EF_MAPPILLAR2, AREA, 'npc_gerhalt$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	disablenpc 'npc_heinrich$;
	npctalk "巴尔蒙德：没办法了，我们必须防止暗达莱斯造成更大的破坏！", 'npc_varmundt$;
	end;
OnTimer73000:
	mapannounce 'map_name$[1], "格哈德的身体发生了变化.", bc_map,0xFFFFFF;
	end;
OnTimer76000:
	specialeffect EF_LORD, AREA, 'npc_gerhalt$;
	disablenpc 'npc_varmundt$;
	mapannounce 'map_name$[1], "巴尔蒙德：没办法了，我们必须防止暗达莱斯造成更大的破坏！", bc_map,0xFFFF00;
	end;
OnTimer80000:
	disablenpc 'npc_gerhalt$;
	donpcevent instance_npcname("#ghmemorialmob07") + "::OnStart";
	stopnpctimer;
	'npc_himelmez$ = 'npc_gerhalt$ = 'npc_heinrich$ = 'npc_varmundt$ = "";
	end;
}

2@gl_k,158,252,4	script	希梅尔麦兹#23	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[希梅尔麦兹]";
	mes "太令人惊讶了, 你们竟然一直让他待在里, 我能对你们说句谢谢嘛?";
	close3;
}

2@gl_k,1,1,0	script	#ghmemorialmob07	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob07");
	monster 'map_name$[1],158,255,"暗达莱斯",2476,1,instance_npcname("#ghmemorialmob07") + "::OnMyMobDead";// MG_AMDARAIS
	'boss_id = $@mobid[0];
	unittalk 'boss_id, "R走开... 不要靠近我...";
	initnpctimer;
	end;
OnTimer5000:
	unittalk 'boss_id, "我不想... 我不想伤害任何人...呃啊...";
	end;
OnTimer10000:
	unittalk 'boss_id, "杀了我! 求求你杀了我！";
	end;
OnTimer16000:
	unittalk 'boss_id, "呃...死吧...去死吧...";
	end;
OnTimer22000:
	unittalk 'boss_id, "毁灭... 死亡！...";
	end;
OnTimer55000:
	donpcevent instance_npcname("#ghmemorialmob08") + "::OnStart";// 巴尔蒙德 buffs and additionnal monsters
	stopnpctimer;
	end;

OnMyMobDead:
	if (mobcount('map_name$[1], instance_npcname("#ghmemorialmob07") + "::OnMyMobDead") < 1) {
		stopnpctimer;
		enablenpc instance_npcname("华金#21");
		enablenpc instance_npcname("#Secret Room Exit");

		stopnpctimer;
		disablenpc instance_npcname("#ghmemorialmob07");
		// note: monsters from #ghmemorialmob08 still alive on Amdarais's dead
	}
	end;
}

// 巴尔蒙德 Buffs
2@gl_k,1,1,0	script	#ghmemorialmob08	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob08");
	initnpctimer;
	end;
OnTimer5000:
	if (unitexists('boss_id) == 0) {
		disablenpc instance_npcname("#ghmemorialmob08");
		stopnpctimer;
		end;
	}
	getunitdata 'boss_id, .@data;
	.@percent_hp = (.@data[UMOB_HP] * 100) / .@data[UMOB_MAXHP];
	mapannounce 'map_name$[1], "暗达莱斯的 HP 达到了 " + .@percent_hp + "% !", bc_map,0x70DBDB;

	// event type every 10%
	switch( .@percent_hp / 10 ) {
	case 10:
		break;
	case 9:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff2") + "::OnEvent";
		break;
	case 8:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"痛苦的卡利斯格",2471,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 7:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"堕落的深渊骑士",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 6:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"血腥骑士",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 5:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff2") + "::OnEvent";
		break;
	case 4:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"徘徊的弓箭手",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 3:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff1") + "::OnEvent";
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"血腥骑士",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 2:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff1") + "::OnEvent";
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"堕落的深渊骑士",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 1:
	case 0:
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff1") + "::OnEvent";
		donpcevent instance_npcname("巴尔蒙德的灵魂#Buff4") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"徘徊的弓箭手",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	}
	end;
OnTimer30000:
	killmonster 'map_name$[1], instance_npcname("#ghmemorialmob08") + "::OnMobDead";
	initnpctimer;
	end;
OnMobDead:
	end;
}

2@gl_k,150,247,5	script	巴尔蒙德的灵魂#Buff1	4_M_BARMUND,2,2,{
	end;
OnTouch:
	specialeffect2 EF_ENHANCE;
	.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	if (.@num == 1)
		sc_start SC_GLASTHEIM_HIDDEN,30000,1,10000,SCSTART_NOTICKDEF;
	else if (.@num == 2)
		sc_start SC_GLASTHEIM_STATE,30000,1,10000,SCSTART_NOTICKDEF;
	else if (.@num == 3) {
		sc_start SC_GLASTHEIM_HEAL,30000,1,10000,SCSTART_NOTICKDEF;
		sc_start SC_GLASTHEIM_HPSP,30000,1,10000,SCSTART_NOTICKDEF;
	}
	else {
		sc_start SC_GLASTHEIM_DEF,30000,1,10000,SCSTART_NOTICKDEF;
		sc_start SC_GLASTHEIM_ITEMDEF,30000,1,10000,SCSTART_NOTICKDEF;
	}
	end;
OnEvent:
	initnpctimer;
	enablenpc instance_npcname( strnpcinfo(0) );
	.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	if (.@num == 1)
		npctalk "巴尔蒙德的灵魂: 暗达莱斯可能使用强大的魔法护盾！靠近我，我会保护你！";
	else if (.@num == 2)
		npctalk "巴尔蒙德的灵魂: 现在是攻击的好机会, 一起进攻!";
	else if (.@num == 3)
		npctalk "巴尔蒙德的灵魂: 如果你不想被安达姆斯的僵尸攻击就靠近我, 我来给你们足够强大的力量!";
	else
		npctalk "巴尔蒙德的灵魂: 如果你不想被安达姆斯的僵尸攻击就靠近我, 我来给你们足够强大的力量!";
	end;
OnTimer10000:
	stopnpctimer;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
2@gl_k,165,247,3	duplicate(巴尔蒙德的灵魂#Buff1)	巴尔蒙德的灵魂#Buff2	4_M_BARMUND,2,2
2@gl_k,150,232,8	duplicate(巴尔蒙德的灵魂#Buff1)	巴尔蒙德的灵魂#Buff3	4_M_BARMUND,2,2
2@gl_k,165,232,2	duplicate(巴尔蒙德的灵魂#Buff1)	巴尔蒙德的灵魂#Buff4	4_M_BARMUND,2,2


2@gl_k,158,241,1	script	华金#21	4_M_SAGE_C,{
	if (checkquest(12319,HUNTING) == 2) {// note: complete hunting 'Corrupted Soul Hunt' prerequis
		mes "[华金]";
		mes "哈哈，暗达莱斯身上找到了这些东西，拿去吧。";
		erasequest 12319;
		setquest 12322;// Space Distortion
		if (isbegin_quest(12321) == 0) {
			setquest 12321;// Time Conqueror
			completequest 12321;
			getitem 6607,5;// Temporal_Crystal
			getitem 6608,5;// Coagulated_Spell
			getexp 350000,350000;
		}
		else {
			getitem 6607,1;// Temporal_Crystal
			getitem 6608,1;// Coagulated_Spell
		}
		next;
		mes "[华金]";
		mes "我已经暂停了时间。但很快，时间裂缝就会消失。";
		specialeffect2 EF_BLIND;
		soundeffect "_blind.wav",0;
		next;
		mes "[华金]";
		mes "可怜的时间困者。";
		mes "他们会无止尽地尝试阻止这一悲剧的发生。";
		next;
		mes "[华金]";
		mes "但是他们每一次都意识不到自己之前的所作所为。";
		mes "悲剧还是会照常发生...";
		next;
		mes "[华金]";
		mes "现在，我会消除你的记忆，也许下一次你会遇见不同的命运。";
		specialeffect2 EF_FREEZE;
		close2;
		warp 'map_name$[1],158,244;
		end;
	}
	mes "[华金]";
	mes "时间裂缝已经开始扭曲了。";
	mes "现在是时候走了！";
	mes "我会把你送出去的！";
	next;
	select("哈...再稍等一会儿...");
	mes "[华金]";
	mes "嗯，还有什么事么...";
	mes "时间裂隙即将关闭，在不出去就来不及了。";
	next;
	if (select("我还想稍微探索一下:请送我出去") == 1) {
		mes "[华金]";
		mes "你确定？这个地方很快就要崩塌了。请尽快回来..";
		close;
	}
	close2;
	warp "glast_01",204,270;
	end;
}

// Treasure Room
//============================================================
1@gl_k,165,136,3	script	奇怪的裂缝#2	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "裂纹看起来可疑，但没有特别的。";
		close;
	}
	specialeffect EF_SPELLBREAKER;

	.@random = rand(1,4);
	switch( atoi(strnpcinfo(2)) ) {
	case 2:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 719,1,"this",165,138;
		makeitem 6608,1,"this",165,138;
		break;
	case 3:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 720,1,"this",159,138;
		if (rand(1,4) == 4)
			makeitem2 15066,1,"this",159,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",159,138;
		makeitem 7229,1,"this",159,138;
		break;
	case 4:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 721,1,"this",153,138;
		if (rand(1,4) == 4)
			makeitem2 13086,1,"this",153,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",153,138;
		makeitem 7230,1,"this",153,138;
		break;
	case 5:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 722,1,"this",147,138;
		if (rand(1,4) == 4)
			makeitem2 2949,1,"this",147,138,0,0,0,0,0,0,0;
		makeitem 6612,1,"this",147,138;
		makeitem 6613,1,"this",147,138;
		makeitem 6608,1,"this",147,138;
		break;
	case 6:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 725,1,"this",141,138;
		makeitem 7228,1,"this",141,138;
		if (rand(1,4) == 4)
			makeitem2 13440,1,"this",141,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",141,138;
		break;
	case 7:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 726,1,"this",135,138;
		if (rand(1,4) == 4)
			makeitem2 2022,1,"this",135,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",135,138;
		break;
	case 8:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 727,1,"this",129,138;
		if (rand(1,4) == 4)
			makeitem2 21007,1,"this",129,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",129,138;
		break;
	case 9:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 727,1,"this",171,138;
		if (rand(1,4) == 4)
			makeitem2 21007,1,"this",171,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",171,138;
		break;
	}
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,159,136,3	duplicate(奇怪的裂缝#2)	奇怪的裂缝#3	CLEAR_NPC
1@gl_k,153,136,3	duplicate(奇怪的裂缝#2)	奇怪的裂缝#4	CLEAR_NPC
1@gl_k,147,136,3	duplicate(奇怪的裂缝#2)	奇怪的裂缝#5	CLEAR_NPC
1@gl_k,141,136,3	duplicate(奇怪的裂缝#2)	奇怪的裂缝#6	CLEAR_NPC
1@gl_k,135,136,3	duplicate(奇怪的裂缝#2)	奇怪的裂缝#7	CLEAR_NPC
1@gl_k,129,136,3	duplicate(奇怪的裂缝#2)	奇怪的裂缝#8	CLEAR_NPC
1@gl_k,171,136,3	duplicate(奇怪的裂缝#2)	奇怪的裂缝#9	CLEAR_NPC

1@gl_k,269,267,3	script	奇怪的裂缝#1	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "裂纹看起来可疑，但没有特别的。";
		close;
	}
	warp 'map_name$[0],149,198;
	end;

OnInstanceInit:
	'map_name$[0] = instance_mapname("1@gl_k");
	'map_name$[1] = instance_mapname("2@gl_k");

	// Entrance
	disablenpc instance_npcname("希梅尔麦兹#1");
	disablenpc instance_npcname("巴尔蒙德#1");
	disablenpc instance_npcname("海因里希#1");
	disablenpc instance_npcname("海因里希#2");
	disablenpc instance_npcname("海因里希#3");

	// Rescue 1
	disablenpc instance_npcname("#ghmemorialmob01");
	disablenpc instance_npcname("候补的服事少年#1");

	// Rescue 2
	disablenpc instance_npcname("#ghmemorialmob02");
	disablenpc instance_npcname("精炼工匠的母亲#1");
	for ( .@i = 1; .@i <= 26; .@i++ )
		disablenpc instance_npcname("已经死掉的人#" + .@i);

	// Sector 3
	disablenpc instance_npcname("#ghmemorialmob03");

	// Root of Corruption
	disablenpc instance_npcname("希梅尔麦兹#2");
	disablenpc instance_npcname("巴尔蒙德#2");
	disablenpc instance_npcname("海因里希#4");
	disablenpc instance_npcname("#Mimelon");
	disablenpc instance_npcname("#ghmemorialmob04");

	// Entrance 2nd map
	disablenpc instance_npcname("巴尔蒙德#21");
	disablenpc instance_npcname("海因里希#21");
	disablenpc instance_npcname("#Servanton");
	hideonnpc instance_npcname("#Servanton_effect");

	// Commanders
	disablenpc instance_npcname("#ghmemorialmob05");
	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ ) {
		disablenpc instance_npcname("#ogh_2-" + .@i);
		disablenpc instance_npcname("#ogh_3-" + .@i);
	}

	// Amdarais Room Entrance
	disablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		disablenpc instance_npcname("腐烂的尸体#" + .@i);

	// Amdarais Spawn
	disablenpc instance_npcname("格哈德#23");
	disablenpc instance_npcname("希梅尔麦兹#22");
	disablenpc instance_npcname("希梅尔麦兹#23");
	disablenpc instance_npcname("巴尔蒙德#23");
	disablenpc instance_npcname("海因里希#23");
	disablenpc instance_npcname("#ghmemorialmob07");

	// 巴尔蒙德 Buffs
	disablenpc instance_npcname("#ghmemorialmob08");
	disablenpc instance_npcname("巴尔蒙德的灵魂#Buff1");
	disablenpc instance_npcname("巴尔蒙德的灵魂#Buff2");
	disablenpc instance_npcname("巴尔蒙德的灵魂#Buff3");
	disablenpc instance_npcname("巴尔蒙德的灵魂#Buff4");
	disablenpc instance_npcname("华金#21");

	// Warps
	disablenpc instance_npcname("#2Control");
	disablenpc instance_npcname("#2Control2");
	disablenpc instance_npcname("#3Control");
	disablenpc instance_npcname("#3Control2");
	disablenpc instance_npcname("#4Control");
	disablenpc instance_npcname("#4Control2");
	disablenpc instance_npcname("#2F Entrance");
	disablenpc instance_npcname("#1 Control");
	disablenpc instance_npcname("#22 Control");
	disablenpc instance_npcname("#22 Control2");
	disablenpc instance_npcname("#23 Control");
	disablenpc instance_npcname("#23 Control2");
	disablenpc instance_npcname("#24 Control");
	disablenpc instance_npcname("#24 Control2");
	disablenpc instance_npcname("#Secret Room Exit");
	end;
}
