//===== rAthena Script =======================================
//= Sarah and Fenrir
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Sarah and Fenrir Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

dali02,97,142,3	script	班哈德博士#a1	4_LGTSCIENCE,{
	if (BaseLevel < 145) {
		mes "[班哈德博士]";
		mes "你现在的级别来这有点危险啊，还是先去锻炼一下吧。";
		close;
	}
	switch( checkquest(9336,PLAYTIME) ) {
	case -1:
		switch( isbegin_quest(9335) ) {
		case 0:// First time doing instance
			mes "[班哈德博士]";
			mes "我是班哈德博士，";
			mes "秀发兹发德共和国最好的武器研究员。";
			next;
			mes "[班哈德博士]";
			mes "经过不断的研究，";
			mes "我创造了各种武器，";
			mes "但我觉得有些东西不见了，";
			mes "所以我找到了古代的文字。";
			mes "我发现了在我的研究中次元装置。";
			next;
			mes "[班哈德博士]";
			mes "我意识到创造更强大先进的武器，";
			mes "需要从过去的辉煌中获取遗物用作材料。";
			mes "这就是我在这里等待你的原因。";
			next;
			mes "[班哈德博士]";
			if (is_party_leader() == false) {
				mes "我渴望成为能制造出真正强大的武器的研究员！";
				mes "为了实现这一点，我们需要 ^0000FF巨人的碎片^000000 ，";
				mes "它曾经出现在芬里厄和莎拉过去的战斗中。";
				next;
				mes "[班哈德博士]";
				mes "我不能离开我的岗位。";
				mes "我需要一个可以为我带回碎片的冒险家，";
				mes "你看起来就很不错啊。";
				next;
				mes "[班哈德博士]";
				mes "我将继续和你们的队长讨论这个问题。";
				close;
			}
			mes "我需要一个冒险家带巨人的碎片回来。";
			mes "你看起来是以为很厉害的冒险家。";
			next;
			mes "[班哈德博士]";
			mes "你觉得怎样？";
			mes "回到过去带 ^0000FF巨人的碎片^000000 回来，";
			mes "我一定会给你足够的报酬。";
			next;
			if (select("不，还是算了", "好吧，我答应你" ) == 1) {
				mes "[班哈德博士]";
				mes "如果你改变主意随时联系我。";
				close;
			}
			mes "[班哈德博士]";
			mes "这里的次元裂缝看来连接过去的克雷斯特汉姆城。";
			mes "那是芬里尔和莎拉的战斗的地方。";
			mes "装置不能维持太久，";
			mes "收集完尽快回来吧。";
			instance_create("Fenrir and Sarah");
			close;
		case 1:
			mes "[班哈德博士]";
			mes "你是否成功的带回了巨人的碎片？";
			mes "你可能面临很多危险，";
			mes "所以谢谢你的努力。";
			next;
			mes "[班哈德博士]";
			mes "我不想在你获得碎片的过程中有任何生命危险";
			if (sarah_fenrir == 0) {
				mes "我给你一个耳环，耳环是我在以前研究中获得的。";
				next;
				mes "- 莎拉的左耳环 - 治愈术1级\n - 莎拉的右耳环 - 瞬间移动1级";
				next;
				.@s = select( "莎拉左耳环", "莎拉右耳环" ) - 1;
			}
			mes "[班哈德博士]";
			mes "你可以用 ^0000FF巨人的碎片^000000 在我助手那里附魔，";
			mes "耳环会获得强大的提升。";
			next;
			mes "[班哈德博士]";
			mes "巨人收集的碎片越多越好。";
			mes "然而次元装置并没有一个稳定的能源供给。";
			next;
			mes "[班哈德博士]";
			mes "使用过以后必须等待一周的时间重新充能，";
			mes "一周后你再来找我吧，";
			mes "如果你想再次寻找巨人的碎片的话。";
			erasequest 9335;// Search for shards of 巨人
			setquest 9336;// Go back to 班哈德博士
			if (isbegin_quest(9337) > 0)
				erasequest 9337;// Wrapping up the Adventure
			if (sarah_fenrir == 0) {
				sarah_fenrir = 1;
				getitem (28310+.@s),1;
			}
			close;
		case 2:
			end;
		}
	case 0:
	case 1:
		erasequest 9336;
		mes "[班哈德博士]";
		mes "我们制造强大的武器缺乏巨人的碎片。因为打开空间裂缝装置需要大量的能量，当能量充满时，再回来找我吧。";
		close;
	case 2:
		mes "[班哈德博士]";
		mes "我一直在等你。最近还好吗？当你离开之后，我无时无刻都在积累能量。我们可以再次打开空间裂缝装置了。";
		next;
		mes "[班哈德博士]";
		mes "我渴望成为能制造出真正强大的武器的研究员！为了实现这一点，我们需要巨人的碎片，它曾经出现在芬里厄和莎拉过去的战斗中。";
		next;
		mes "[班哈德博士]";
		mes "我不能离开我的岗位。我需要一个可以为我带回碎片的冒险家，你看起来就很不错啊。";
		next;
		mes "[班哈德博士]";
		mes "你觉得怎样？回到过去带 ^0000FF巨人的碎片^000000 回来，我一定会给你足够的报酬。";
		if (is_party_leader() == true) {
			next;
			if (select("不，还是算了", "好吧，我答应你" ) == 1) {
				mes "[班哈德博士]";
				mes "这里的次元裂缝看来连接到了过去的克雷斯特汉姆城。那是芬里尔和莎拉的战斗的地方。装置不能维持太久，收集完尽快回来吧。";
				close;
			}
			instance_create("Fenrir and Sarah");
		}
		close;
	}
}

dali02,99,148,3	script	次元装置#XX1	PORTAL,{
	if (BaseLevel < 145) {
		mes "[次元装置]";
		mes "必须大于145级才能开启裂缝。";
		close;
	}
	switch( checkquest(9336,PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		mes "- 次元装置已经关闭了. 看起来像是不能再进入了. -";
		close;
	case 2:
		break;
	}
	if (isbegin_quest(9335) > 0) {
		mes "- 次元装置已经关闭了. 看起来像是不能再进入了. -";
		close;
	}
	if (select( "不进入裂缝.", "进入裂缝" ) == 1) {
		if (getcharid(1) > 0) {
			mes "- 离开了次元装置 -";
			close;
		}
		mes "-这是一个次元装置. -";
		close;
	}
	.@md_name$ = "Fenrir and Sarah";
	switch( instance_enter(.@md_name$) ) {
	case IE_OTHER:
		mes "发生一个未知错误。";
		close;
	case IE_NOINSTANCE:
		mes "副本进度 "+.@md_name$+" 不存在。 队长还没有建立副本进度。";
		close;
	case IE_NOMEMBER:
		mes "只有注册过的成员可以进入副本 "+"莎拉与芬里尔"+"。";
		close;
	case IE_OK:
		mapannounce "dali02",getpartyname(getcharid(1))+"小队成员 "+strcharinfo(0)+" 进入副本莎拉与芬里尔。",bc_map,"0x00ff99";
		setquest 9335;// Search for shards of 巨人
		if (isbegin_quest(9336) > 0)
			erasequest 9336;
		// warp "1@glast",367,304;
		end;
	}
}

dali02,93,146,6	script	助理教授#a1	4_M_REPAIR,{
	if (sarah_fenrir == 0){
		mes "班哈德博士是我最尊敬的教授。";
		mes "你有你尊敬的人吗？";
		close;
	}	
	disable_items;
	mes "[首席助理]";
	mes "我从教授那里听说过你。";
	mes "他告诉我如果你能带回一些 ^0000FF巨人的碎片^000000 和我交换的话，";
	mes "我可以给你兑换成饰品或者附魔。";
	next;
	switch( select( "附魔饰品.", "兑换饰品", "强化莎拉战斗长袍","附魔觉醒莎拉长袍","退出对话" ) ) {
	case 1:
		mes "[首席助理]";
		mes "我们在考古现场发现的莎拉耳环，";
		mes "它肯定有很大的潜力。";
		next;
		switch( select( "附魔莎拉耳环", "重置莎拉耳环附魔", "退出对话" ) ) {
		case 1:
			//mes "[首席助理]";
			//mes "一个耳环可以附魔两次。你想附魔哪个耳环？";
			//next;
			//switch( select( "莎拉的左耳环", "莎拉的有耳环", "退出" ) ) {
			//case 1:
			//	.@sarah_item_id = 28310;
			//	.@sarah_earring$ = "左";
			//	break;
			//case 2:
			//	.@sarah_item_id = 28311;
			//	.@sarah_earring$ = "右";
			//	break;
			//case 3:
			//	mes "[首席助理]";
			//	mes "如果你想在莎拉耳环上附魔，再来找我吧。";
			//	close;
			//}
			mes "[首席助理]";
			mes "你必须装备才能附魔。";
			mes "您希望附魔哪个栏目？";
			next;
			if (select( "附魔饰品(左)-"+getequipname(EQI_ACC_L),"附魔饰品(右)-"+getequipname(EQI_ACC_R) ) == 1) {
				.@part = EQI_ACC_L;
				.@column$ = "左";
				.@sarah_item_id = getequipid(EQI_ACC_L);
				.@bonus_size = 4;		// 4 possibilities
				.@bonus_chance = 20;	// 20% per possibility
			}
			else {
				.@part = EQI_ACC_R;
				.@column$ = "右";
				.@sarah_item_id = getequipid(EQI_ACC_R);
				.@bonus_size = 6;		// 6 possibilities
				.@bonus_chance = 10;	// 10% per possibility
			}
			.@equip_id = getequipid(.@part);
			if (.@equip_id != 28310 && .@equip_id != 28311) {
				mes "[首席助理]";
				mes "为莎拉耳环附魔, ";
				mes "必须先装备沙拉耳环.";
				close;
			}
			setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
			if (.@card[2] > 0) {// 2 enchants
				mes "[首席助理]";
				mes "这个耳环已经不能再附魔了。";
				close;
			}
			if (.@card[3] > 0) {// 1 enchant
				mes "[首席助理]";
				mes "现在为莎拉耳环的第4个孔附魔。";
				.@slot = 3;
			}
			else {// none enchant
				mes "[首席助理]";
				mes "现在为莎拉耳环的第3个孔附魔";
				.@slot = 4;
			}
			next;
			mes "[首席助理]";
			mes "你想要在装备的第 " + .@slot + " 个孔上附魔什么属性呢？";
			next;
			switch( select( "暴击/暴击伤害", "名弓/FLEE", "降低SP消耗/MATK", "ASPD/技能后延时" ) ) {
			case 1:
				setarray .@bonus[0],
					4863,	// Fatal1
					4864,	// Fatal2
					4939,	// Critical2
					4940,	// Critical3
					4941,	// Critical4
					4865;	// Fatal3
				mes "[首席助理]";
				mes "你选择了为第 " + .@slot + " 个孔上附魔暴击或暴击伤害的附魔.";
				break;
			case 2:
				setarray .@bonus[0],
					4832,	// Expert_Archer1
					4833,	// Expert_Archer2
					4942,	// Dodge1
					4943,	// Dodge2
					4944,	// Dodge3
					4834;	// Expert_Archer3
				mes "[首席助理]";
				mes "你选择了为第 " + .@slot + " 个孔上附增加远程伤害或回避率的附魔.";
				break;
			case 3:
				setarray .@bonus[0],
					4945,	// Thrift1
					4946,	// Thrift2
					4897,	// Matk3p
					4898,	// Matk4p
					4947,	// Thrift3
					4899;	// Matk5p
				mes "[首席助理]";
				mes "你选择了为第 " + .@slot + " 个孔上附增加降低SP消耗或魔法攻击力的附魔.";
				break;
			case 4:
				setarray .@bonus[0],
					4869,	// Attack_Delay_1
					4872,	// Attack_Delay_2
					4948,	// Skill_Delay1
					4949,	// Skill_Delay2
					4950,	// Skill_Delay3
					4873;	// Attack_Delay_3
				mes "[首席助理]";
				mes "你选择了为第 " + .@slot + " 个孔上附增加攻速或减少技能冷却时间.";
				break;
			}
			next;
			mes "[首席助理]";
			mes "需要使用 巨人的碎片 x 4 交换。";
			next;
			mes "[首席助理]";
			mes "附魔风险比较高，";
			mes "莎拉耳环有被损毁的风险？";
			mes "确定要附魔吗？";
			next;
			if (select( "我再想想", "是的，附魔" ) == 1) {
				mes "[首席助理]";
				mes "如果下次想要附魔的话，";
				mes "欢迎随时来找我！";
				close;
			}
			if (countitem(6803) < 4) {
				mes "[首席助理]";
				mes "没有足够的碎片的巨人。";
				mes "需要消耗 巨人的碎片 x 4 才能附魔一次。";
				close;
			}
			mes "[首席助理]";
			mes "好的。";
			mes "我将开始附魔！";
			mes "祝你好运~！";
			next;
			mes "[首席助理]";
			mes "砰砰砰！！";
			next;
			progressbar "000000",5;
			specialeffect2 EF_MAGICALATTHIT;
			specialeffect2 EF_POTION2;
			delitem 6803,4;// Shard of 巨人

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]))
				close;

			delequip .@part;
			.@r = rand(100);
			for ( .@i = 0; .@i < .@bonus_size && (.@bonus_chance * (.@i+1)) < .@r; .@i++ );
			if (.@i == .@bonus_size) {
				specialeffect2 EF_CRASHEARTH;
				specialeffect2 EF_MAXPOWER;
				mes "[首席助理]";
				mes "悲哀地...它是..摧毁...呼..附魔不是一件容易的事。";
				close;
			}
			.@card[.@slot-1] = .@bonus[.@i];
			specialeffect2 EF_ANGEL2;
			getitem2 .@sarah_item_id,1,1,0,0,.@card[0],.@card[1],.@card[2],.@card[3];
			mes "[首席助理]";
			mes "附魔完成！";
			close;
		case 2:
			mes "[首席助理]";
			mes "您想初始化哪一个耳环？";
			next;
			//switch( select( "The left earring of Sarah", "The right earring of Sarah", "Quit" ) ) {
			//case 1:
			//	.@sarah_item_id = 28310;
			//	.@sarah_earring$ = "left";
			//	break;
			//case 2:
			//	.@sarah_item_id = 28311;
			//	.@sarah_earring$ = "right";
			//	break;
			//case 3:
			//	mes "[首席助理]";
			//	mes "如果你想在莎拉耳环上初始化一个结界，再来找我吧。";
			//	close;
			//}
			mes "[首席助理]";
			mes "初始化需要 巨人的碎片 x 1 作为交换..";
			next;
			if (countitem(6803) < 1) {
				mes "[首席助理]";
				mes "没有足够的碎片的巨人，";
				mes "初始化必须用 ^0000FF巨人的碎片^000000 来当做加工费用。";
				close;
			}
			mes "[首席助理]";
			mes "您必须装备该项使初始化成为可能。";
			mes "您希望初始化哪一列的耳环？";
			next;
			if (select( "附魔饰品(左)-"+getequipname(EQI_ACC_L),"附魔饰品(右)-"+getequipname(EQI_ACC_R) ) == 1) {
				.@part = EQI_ACC_L;
				.@column$ = "左";
			}
			else {
				.@part = EQI_ACC_R;
				.@column$ = "右";
			}
			.@equip_id = getequipid(.@part);
			setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
			if (.@equip_id == -1) {
				mes "[首席助理]";
				mes "要初始化结界，相关的列必须装备齿轮。";
				close;
			}
			if (getequipcardid(.@part,3) < 1) {
				mes "[首席助理]";
				mes "没有附魔过的莎拉耳环无法初始化。";
				close;
			}
			mes "[首席助理]";
			mes "你选择了 装备在"+ .@column$ + " 侧的 " + getequipname(.@part) + ", 你确认要为这件装备重置附魔吗?";
			next;
			if (select( "不是", "是" ) == 2) {
				mes "[首席助理]";
				mes "那么初始化将开始。";
				next;
				progressbar "000000",5;
				specialeffect2 EF_MAGICALATTHIT;
				specialeffect2 EF_POTION2;
				specialeffect2 EF_ANGEL2;
				delitem 6803,1;// Shard of 巨人

				// anti-hack
				if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]))
					close;

				delequip .@part;
				getitem .@equip_id,1;
				mes "[首席助理]";
				mes "初始化完成。如果需要其他服务的话, 欢迎随时来找我。";
			}
			close;
		case 3:
			mes "[首席助理]";
			mes "如果你想在莎拉耳环上附魔，再来找我吧。";
			close;
		}
	case 2:
		mes "[首席助理]";
		mes "这里是独特的和罕见的耳环，它是我与教授在克雷斯特汉姆城考古中发现。";
		next;
		mes "[首席助理]";
		mes "我认为这是莎拉过去的饰品，似乎有很大的潜力。";
		next;
		mes "[首席助理]";
		mes "我会以 巨人的碎片 x 1 的代价跟你交换耳环。";
		next;
		mes "[首席助理]";
		mes "- 莎拉左耳环 -治疗1lv ";
		mes "- 莎拉右耳环 -瞬间移动1lv ";
		mes "你想兑换哪一种耳环呢？";
		next;
		.@s = select( "莎拉左耳环", "莎拉有耳环", "退出对话" ) - 1;
		if (.@s < 2) {
			if (countitem(6803) < 1) {
				mes "[首席助理]";
				mes "没有足够的巨人的碎片。";
				mes "你帮我收集到 ^0000FF巨人的碎片^000000 ，";
				mes "我才能跟你交换耳环。";
				close;
			}
			delitem 6803,1;// Shard of 巨人
			getitem (28310+.@s),1;
		}
		mes "[首席助理]";
		mes "班哈德博士是我最尊敬的教授。";
		mes "你有你尊敬的人吗？";
		close;
	case 3:
		mes "[首席助理]";
		mes "我们最近的研究成果有了新的进展。";
		mes "如果你有^FF0000+13^000000以上^0000FF莎拉战斗长袍^000000。";
		mes "我们可以为你引导出长袍的全部力量！";
		mes "但是^FF0000原装备上的卡片，精炼度，附魔效果等都会消失^000000。";
		next;
		if(select("兑换^0000FF觉醒莎拉长袍^000000","取消") == 2) {
			mes "[首席助理]";
			mes "明白了。";
			close;
		}
		.@part = EQI_ARMOR;
		if(getequipid(.@part) != 15121) {
			mes "[首席助理]";
			mes "要兑换的话，把^0000FF莎拉战斗长袍^000000穿上吧。";
			close;
		}
		if(getequiprefinerycnt(.@part) < 13) {
			mes "[首席助理]";
			mes "装备中的^0000FF莎拉战斗长袍^000000精炼度不足^FF000013^000000，无法兑换。";
			close;
		}
		delequip .@part;
		getitem 15157,1;
		mes "[首席助理]";
		mes "兑换成功！";
		mes "尽情享受这件装备的强大力量吧！";
		close;

	case 4:
		mes "[首席助理]";
		mes "其实^0000FF觉醒莎拉长袍^000000还可以附魔！";
		mes "但这样做，会给装备带来极大的负担。";
		mes "因此附魔需要^FF0000+13^000000以上的^0000FF觉醒的莎拉长袍^000000。";
		next;
		if(select("附魔^0000FF觉醒莎拉长袍^000000","取消") == 2) {
			mes "[首席助理]";
			mes "明白了。";
			close;
		}
		.@part = EQI_ARMOR;
		.@id = getequipid(.@part);
		.@equip_refine = getequiprefinerycnt(.@part);
		setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
		if(.@id != 15157) {
			mes "[首席助理]";
			mes "我只能为^0000FF觉醒莎拉长袍^000000附魔。";
			mes "如果你有的话，先装备上吧。";
			close;
		}
		if(.@equip_refine < 13) {
			mes "[首席助理]";
			mes "装备中的^0000FF觉醒莎拉长袍^000000精炼度不足^FF000013^000000，无法附魔。";
			close;
		}
		delequip .@part;
		getitem2 .@id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],4876;
		mes "[首席助理]";
		mes "附魔完毕！";
		mes "尽情享受这件装备的强大力量吧！";
		close;
	case 5:
		mes "[首席助理]";
		mes "我从教授那里听说过你。";
		mes "他告诉我如果你能带回一些 ^0000FF巨人的碎片^000000 的话，";
		mes "我可以帮你在莎拉耳环上附魔。";
		close;
	}
}

1@glast,360,295,0	script	#glast_event_1	HIDDEN_WARP_NPC,8,8,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_event_1");
	setpcblock PCBLOCK_NPC, true;
	specialeffect2 EF_HIT2;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 你好~！你也是冒险者吗？";
	sleep2 3000;
	cutin "fenrir_a.bmp",2;
	npctalk "芬里尔：克雷斯特汉姆看起来有些奇怪...以前有这样的黑暗气息么？", 'fenrir_1$;
	sleep2 3000;
	npctalk "芬里尔：不好意思，待会儿再打招呼吧，好像有魔兽靠近了！", 'fenrir_1$;
	sleep2 3000;
	cutin "",255;
	specialeffect EF_LOCKON, AREA, 'fenrir_1$;
	.@label$ = instance_npcname("#glast_event_1") + "::OnMobDead";
	monster 'map_glast$,355,300, "Mutant Galion",3198,1, .@label$;	// MM_M_GALION
	monster 'map_glast$,358,300, "Mutant Galion",3198,1, .@label$;
	monster 'map_glast$,361,300, "Mutant Galion",3198,1, .@label$;
	monster 'map_glast$,355,289, "Mutant Galion",3198,1, .@label$;
	monster 'map_glast$,358,289, "Mutant Galion",3198,1, .@label$;
	monster 'map_glast$,364,294, "Mutant Galion",3198,1, .@label$;
	monster 'map_glast$,364,297, "Mutant Galion",3198,1, .@label$;
	monster 'map_glast$,364,291, "Mutant Galion",3198,1, .@label$;
	monster 'map_glast$,353,290, "Mutant Galion",3198,1, .@label$;
	unittalk getcharid(3), "" + strcharinfo(0) + " : What are these things?";
	npctalk "芬里尔：请你为我的法术争取一点时间！", 'fenrir_1$;
	donpcevent instance_npcname("#glast_event_1") + "::OnBar";
	donpcevent instance_npcname("#glast_event_1") + "::OnEvent";
	setpcblock PCBLOCK_NPC, false;
	end;
OnBar:
	progressbar_npc "000000",10, 'fenrir_1$;
	end;

OnEvent:
	// npctalk "芬里尔: Buy me some time while I cast magic! I will handle this!", 'fenrir_1$;// double on official
	sleep 3000;
	npctalk "芬里尔：雷之精灵，请听从我的呼唤！", 'fenrir_1$;
	sleep 3000;
	npctalk "芬里尔：劈开暗影，审判降临！", 'fenrir_1$;
	sleep 3000;
	npctalk "芬里尔：风之精灵，召唤暴雪的来临！", 'fenrir_1$;
	sleep 3000;
	npctalk "芬里尔：雷爆术！", 'fenrir_1$;
	for ( .@i = 1; .@i < 8; .@i++ )
		specialeffect EF_THUNDERSTORM, AREA, instance_npcname( "#effect_glast_event01_" + .@i );
	killmonster 'map_glast$, instance_npcname("#glast_event_1") + "::OnMobDead";
	sleep 4000;
	npctalk "芬里尔：克雷斯特汉姆是过去的圣殿...", 'fenrir_1$;
	sleep 3000;
	npctalk "芬里尔：但如今已成为魔兽的栖息地了吗？", 'fenrir_1$;
	sleep 3000;
	npctalk "芬里尔：等等，好像还有什么不对劲...", 'fenrir_1$;
	sleep 3000;
	npctalk "芬里尔：鬣狗不应该出现在这种地方。", 'fenrir_1$;
	sleep 3000;
	enablenpc instance_npcname("#glast_event_3");
	end;

OnMobDead:
	end;
}

1@glast,360,295,0	script	#glast_event_3	HIDDEN_WARP_NPC,8,8,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_event_3");
	setpcblock PCBLOCK_NPC, true;
	cutin "fenrir_a.bmp",2;
	npctalk "芬里尔：虽然很突然，但谢谢你帮助我。", 'fenrir_1$;
	sleep2 2000;
	npctalk "芬里尔：冒昧的问一句，你来这是为了什么呢？", 'fenrir_1$;
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 我是冒险者...";
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 通过时间与空间的裂缝来到此地...";
	sleep2 2000;
	npctalk "芬里尔：是吗？原来如此。和这个世界没有什么联系啊...", 'fenrir_1$;
	sleep2 2000;
	npctalk "芬里尔：这个地方非常危险，趁着还不迟，快点回去吧。", 'fenrir_1$;
	sleep2 2000;
	npctalk "芬里尔：我有必须要寻找的东西，不能再浪费时间了。", 'fenrir_1$;
	sleep2 2000;
	npctalk "芬里尔：先谢谢你！我希望我们以后能再见面！", 'fenrir_1$;
	sleep2 3000;
	cutin "",255;
	specialeffect EF_LOCKON, AREA, 'fenrir_1$;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 那人.. 是芬里尔吗?.. 运气这么好....? 也或许.. 不是吧..";
	mapannounce .@map$,"芬里尔往南边去了，跟上她吧！",bc_map,"0xFFFF00";
	disablenpc 'fenrir_1$;
	for ( .@i = 1; .@i < 8; .@i++ )
		disablenpc instance_npcname("#effect_glast_event01_" + .@i);
	enablenpc instance_npcname("#glast_move_01");
	enablenpc instance_npcname("#glast_event_5");
	enablenpc 'fenrir_2$;
	setpcblock PCBLOCK_NPC, false;
	end;
}

1@glast,359,296,4	script	#effect_glast_event01_1	CLEAR_NPC,{ end; }
1@glast,353,294,4	duplicate(#effect_glast_event01_1)	#effect_glast_event01_2	CLEAR_NPC
1@glast,360,304,4	duplicate(#effect_glast_event01_1)	#effect_glast_event01_3	CLEAR_NPC
1@glast,366,295,4	duplicate(#effect_glast_event01_1)	#effect_glast_event01_4	CLEAR_NPC
1@glast,352,304,4	duplicate(#effect_glast_event01_1)	#effect_glast_event01_5	CLEAR_NPC
1@glast,356,284,4	duplicate(#effect_glast_event01_1)	#effect_glast_event01_6	CLEAR_NPC
1@glast,345,283,4	duplicate(#effect_glast_event01_1)	#effect_glast_event01_7	CLEAR_NPC

1@glast,359,294,4	duplicate(#effect_glast_event01_1)	芬里尔#glast_01	4_F_FENRIR

// Warps
// From entrance
1@glast,352,279,0	script	#glast_move_01	WARPNPC,4,4,{
	end;
OnTouch:
	if ('back_to_entrance == 0)
		warp 'map_glast$,347,265;
	else {
		disablenpc instance_npcname("#glast_move_01");
		initnpctimer;
	}
	end;

OnTimer1500:
	specialeffect EF_FIREPILLARON2, AREA, 'sarah_ego$;
	end;
OnTimer2000:
	hideoffnpc 'sarah_ego$;
	specialeffect EF_WHITEBODY, AREA, 'sarah_ego$;
	end;
OnTimer3000:
	npctalk "莎拉艾琳：令人印象深刻的芬里尔, you cleverly escaped", 'sarah_ego$;
	end;
OnTimer7500:
	npctalk "莎拉艾琳：采购经理…今天我要让你站起来", 'sarah_ego$;
	end;
OnTimer8000:
	npctalk "芬里尔：今天我希望结束我们的不幸的关系。", 'fenrir_11$;
	end;
OnTimer11000:
	npctalk "莎拉艾琳：但是, do not think this is the end", 'sarah_ego$;
	end;
OnTimer14000:
	npctalk "莎拉艾琳：你一定还会再见到我的！！！", 'sarah_ego$;
	end;
OnTimer14500:
	npctalk "芬里尔：毫米…我期待着它，莎拉艾琳…", 'fenrir_11$;
	specialeffect EF_FIREPILLARON2, AREA, 'sarah_ego$;
	end;
OnTimer15500:
	stopnpctimer;
	disablenpc 'sarah_ego$;
	end;
}

// Fenrir left side
// 1@glast,42,350,0	warp2	#glast_event_warp_1	10,10,1@glast,105,369
// 1@glast,52,346,0	warp2	#glast_event_warp_2	10,10,1@glast,105,369
// 1@glast,35,364,0	warp2	#glast_event_warp_3	10,10,1@glast,105,369
// 1@glast,41,364,0	warp2	#glast_event_warp_4	10,10,1@glast,105,369
// 1@glast,44,371,0	warp2	#glast_event_warp_5	10,10,1@glast,105,369
// 1@glast,54,362,0	warp2	#glast_event_warp_6	10,10,1@glast,105,369
// 1@glast,44,358,0	warp2	#glast_event_warp_7	10,10,1@glast,105,369

// To stairs final room
1@glast,199,237,0	script	#glast_move_03	WARPNPC,2,2,{
	end;
OnTouch:
	if (is_party_leader() == true) {
		// warpparty 'map_glast$,197,263, getcharid(1), 'map_glast$,1,1;	// using mapwarp for now
		mapwarp 'map_glast$,'map_glast$,197,263;
	}
	end;
}

// To final room
1@glast,199,294,0	script	#glast_move_03a	WARPNPC,3,3,{
	end;
OnTouch:
	if (is_party_leader() == true) {
		// warpparty 'map_glast$,188,334, getcharid(1), 'map_glast$,1,1;
		mapwarp 'map_glast$,'map_glast$,188,334;
	}
	end;
}

// Exit final room
//1@glast,189,331,0	warp2	#glast_event_warp_11	10,10,1@glast,197,221
//1@glast,206,337,0	warp2	#glast_event_warp_12	10,10,1@glast,197,221
//1@glast,205,330,0	warp2	#glast_event_warp_13	10,10,1@glast,197,221
//1@glast,193,337,0	warp2	#glast_event_warp_14	10,10,1@glast,197,221
//1@glast,210,331,0	warp2	#glast_event_warp_15	10,10,1@glast,197,221
//1@glast,194,330,0	warp2	#glast_event_warp_16	10,10,1@glast,197,221

// Back to entrance
1@glast,351,269,0	script	#glast_move_04	WARPNPC,3,3,{
	end;
OnTouch:
	if (is_party_leader() == true) {
		donpcevent instance_npcname("#sarah_wrath") + "::OnStop";
		// warpparty 'map_glast$,349,282, getcharid(1), 'map_glast$,1,1;
		mapwarp 'map_glast$,'map_glast$,349,282;
	}
	end;
}

// Exit
1@glast,376,303,0	warp2	#glast_move_05	2,2,dali02,91,140

// Fenrir bottom
1@glast,216,48,0	script	#glast_event_5	HIDDEN_WARP_NPC,6,6,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_event_5");
	setpcblock PCBLOCK_NPC, true;
	cutin "fenrir_a.bmp",2;
	npctalk "芬里尔：你还在这里徘徊？很危险的，你还有什么事吗？", 'fenrir_2$;
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 我知道很危险，但觉得很有趣，不知不觉就...";
	sleep2 2000;
	npctalk "芬里尔：这就是所谓的冒险家精神么...", 'fenrir_2$;
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 不用担心，我能保护好自己。";
	sleep2 2000;
	npctalk "芬里尔：是吗？看起来挺有余力的样子...那么能帮助我吗？", 'fenrir_2$;
	sleep2 2000;
	npctalk "芬里尔：在这里有我想要寻找的东西...", 'fenrir_2$;
	viewpoint 1,47,270,1,0xFF0000;
	sleep2 2000;
	npctalk "芬里尔：我能感受到它的力量，在11点方向。", 'fenrir_2$;
	sleep2 2000;
	npctalk "芬里尔：我先走一步，5分钟内在那汇合吧。", 'fenrir_2$;
	sleep2 2000;
	npctalk "芬里尔：如果你来晚的话，我只能一个人先去了。", 'fenrir_2$;
	sleep2 2000;
	npctalk "芬里尔：当然你可以自由选择是否帮助我，那么一会再见！", 'fenrir_2$;
	sleep2 3000;
	cutin "",255;
	disablenpc 'fenrir_2$;
	sleep2 1000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 怎么办呢？还要完成班哈德博士的委托...";
	sleep2 2000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 是帮助她，还是先去搜集巨人的碎片？";
	sleep2 2000;
	donpcevent instance_npcname("#fenrir_left_final_2") + "::OnStart";// timer (5 mins)
	for ( .@i = 1; .@i < 7; .@i++ )
		enablenpc instance_npcname("Ancient 巨人#glast_" + .@i);
	enablenpc 'fenrir_3$;
	enablenpc instance_npcname("#glast_event_7");
	disablenpc instance_npcname("#glast_move_01");	// officially player can be stucked in entrance
	setpcblock PCBLOCK_NPC, false;
	end;
}

1@glast,216,52,6	script	芬里尔#glast_02	4_F_FENRIR,{ end; }

// Fenrir left side, entrance
1@glast,47,270,0	script	#glast_event_7	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	if (is_party_leader() == false)
		npctalk "芬里尔：来了么？谢谢你们。", 'fenrir_3$;
	else {
		setpcblock PCBLOCK_NPC, true;
		disablenpc instance_npcname("#glast_event_7");
		npctalk "芬里尔：你来了！我等你很久了！", 'fenrir_3$;
		sleep2 2000;
		npctalk "芬里尔：时间不多了，这边走。", 'fenrir_3$;
		sleep2 2000;
		setpcblock PCBLOCK_NPC, false;
		if ('skip_left_part == 0 && is_party_leader() == true) {// 5 mins have passed?
			donpcevent instance_npcname("#fenrir_left_final_2") + "::OnStop";
			disablenpc 'fenrir_3$;
			enablenpc 'fenrir_4$;
			enablenpc instance_npcname("#glast_event_9");
			// warpparty 'map_glast$,41,348, getcharid(1), 'map_glast$,1,1;
			mapwarp 'map_glast$,'map_glast$,41,348;
		}
	}
	end;
}

1@glast,47,270,4	duplicate(#effect_glast_event01_1)	芬里尔#glast_03	4_F_FENRIR

// Fenrir left side, inside
1@glast,44,357,0	script	#glast_event_9	HIDDEN_WARP_NPC,3,3,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_event_9");
	setpcblock PCBLOCK_NPC, true;
	cutin "fenrir_a.bmp",2;
	npctalk "芬里尔：神剑巴尔姆...近千年过去了，还是完好无损的在这里...", 'fenrir_4$;
	sleep2 3000;
	npctalk "芬里尔：我能感觉到在这个地方..邪恶的怪物好像被某人控制一样。", 'fenrir_4$;
	sleep2 3000;
	npctalk "芬里尔：是有人提前来到了这里吗？", 'fenrir_4$;
	sleep2 3000;
	npctalk "芬里尔：先不管这么多，我们要提前拿到神剑巴尔姆...", 'fenrir_4$;
	sleep2 3000;
	enablenpc 'sarah_10$;
	enablenpc instance_npcname("巨人#semi1");
	enablenpc instance_npcname("巨人#semi2");
	sleep2 1000;
	cutin "sarah_hero3.bmp",0;
	npctalk "莎拉：哈哈，果然不出我所料,你会找到这里。", 'sarah_10$;
	sleep2 3000;
	npctalk "莎拉：这把剑就是关键线索么？", 'sarah_10$;
	sleep2 3000;
	npctalk "莎拉：用它就可以解开千年的封印吧？芬里尔！", 'sarah_10$;
	sleep2 3500;
	unittalk getcharid(3), "" + strcharinfo(0) + " : 他应该就是真正的芬里尔吧？";
	npctalk "芬里尔：神剑巴尔姆...近千年过去了，还是完好无损的在这里...", 'fenrir_4$;
	cutin "fenrir_b.bmp",2;
	sleep2 5000;
	npctalk "莎拉：我叫莎拉 艾琳，女神芙蕾亚的十二祭司之一。", 'sarah_10$;
	cutin "sarah_hero3.bmp",0;
	sleep2 3000;
	npctalk "莎拉：为了达成芙蕾亚大人的心愿，这把剑我就收下了。", 'sarah_10$;
	npctalk "芬里尔：两个巨人！这不是一场容易的战斗。帮助我战胜巨人！", 'fenrir_4$;
	cutin "fenrir_b.bmp",2;
	sleep2 3000;
	npctalk "莎拉艾琳：上吧！巨人！我忠诚的怪物！", 'sarah_10$;
	npctalk "芬里尔：莎拉隐藏起来了不断召唤怪物！我会打那些召唤出来的怪物。你来招呼这两只巨人吧!", 'fenrir_4$;
	cutin "fenrir_b.bmp",255;
	sleep2 2000;
	disablenpc 'sarah_10$;
	disablenpc instance_npcname("巨人#semi1");
	disablenpc instance_npcname("巨人#semi2");
	enablenpc instance_npcname("#fenrir_left_final_1");
	monster 'map_glast$,49,363, "巨人#semi1", 3191,1, instance_npcname("#fenrir_left_final_1") + "::OnMobDead";// MM_M_GIGAN1
	monster 'map_glast$,41,363, "巨人#semi2", 3192,1, instance_npcname("#fenrir_left_final_1") + "::OnMobDead";// MM_M_GIGAN2
	donpcevent instance_npcname("#glast_event_9") + "::OnEvent";
	setpcblock PCBLOCK_NPC, false;
	end;
OnEvent:
	sleep 15000;
	'timer_left_side = 0;
	donpcevent instance_npcname("#glast_event_9") + "::OnStart";
	end;

OnStart:
	initnpctimer;
	npctalk "芬里尔：雷之精灵在空气舞动！请听从我的呼唤！", 'fenrir_4$;
	callsub S_Spawn,1,3198;// MM_M_GALION

	progressbar_npc "000000",5, 'fenrir_4$;	// note: display effect after dead of 巨人
	npctalk "芬里尔：雷爆术！！！", 'fenrir_4$;
	for ( .@i = 1; .@i < 9; .@i++ )
		specialeffect EF_LIGHTBOLT, AREA, instance_npcname("#effect_glast_event02_" + .@i);
	end;
OnTimer6000:
	killmonster 'map_glast$, instance_npcname("#glast_event_9") + "::OnMobDead";
	end;
OnTimer15000:
	npctalk "芬里尔：吞噬宇宙的火精灵！", 'fenrir_4$;
	callsub S_Spawn,2,3198;// MM_M_GALION
	progressbar_npc "000000",5, 'fenrir_4$;
	npctalk "芬里尔：地狱之火，吞咽空间！流星风暴~！！！！！", 'fenrir_4$;
	for ( .@i = 1; .@i < 9; .@i++ )
		specialeffect EF_METEORSTORM, AREA, instance_npcname("#effect_glast_event02_" + .@i);
	end;
OnTimer21000:
	killmonster 'map_glast$, instance_npcname("#glast_event_9") + "::OnMobDead";
	end;
OnTimer30000:
	npctalk "芬里尔：雷之精灵在空气舞动！请听从我的呼唤！", 'fenrir_4$;
	callsub S_Spawn,1,3199;// MM_M_MUTANT_DRAGON
	progressbar_npc "000000",5, 'fenrir_4$;
	npctalk "芬里尔：雷爆术！！！", 'fenrir_4$;
	for ( .@i = 1; .@i < 9; .@i++ )
		specialeffect EF_LIGHTBOLT, AREA, instance_npcname("#effect_glast_event02_" + .@i);
	end;
OnTimer36000:
	killmonster 'map_glast$, instance_npcname("#glast_event_9") + "::OnMobDead";
	end;
OnTimer45000:
	npctalk "芬里尔：风舞冰霜，风之精灵！！！", 'fenrir_4$;
	callsub S_Spawn,2,3199;// MM_M_MUTANT_DRAGON
	progressbar_npc "000000",5, 'fenrir_4$;
	npctalk "芬里尔：风暴可以冻结灵魂！暴风雪舞~！！！！！", 'fenrir_4$;
	for ( .@i = 1; .@i < 9; .@i++ )
		specialeffect EF_STORMGUST, AREA, instance_npcname("#effect_glast_event02_" + .@i);
	end;
OnTimer51000:
	killmonster 'map_glast$, instance_npcname("#glast_event_9") + "::OnMobDead";
	end;
OnTimer55000:
	'timer_left_side++;
	if ('timer_left_side == 5) {
		stopnpctimer;
		donpcevent instance_npcname("#fenrir_left_vermilion") + "::OnEvent";
	}
	end;
OnTimer60000:
	donpcevent instance_npcname("#glast_event_9") + "::OnStart";
	end;

OnStop:
	stopnpctimer;
	end;

OnMobDead:
	end;

S_Spawn:
	.@mob_id = getarg(1);
	.@label$ = instance_npcname("#glast_event_9") + "::OnMobDead";
	if (getarg(0) == 1) {
		monster 'map_glast$,43,355, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,39,361, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,48,361, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,43,363, "--ja--", .@mob_id,1, .@label$;
	}
	else {
		monster 'map_glast$,37,360, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,37,362, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,37,364, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,37,366, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,37,368, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,41,371, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,50,360, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,50,362, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,50,364, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,50,366, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,50,368, "--ja--", .@mob_id,1, .@label$;
		monster 'map_glast$,46,371, "--ja--", .@mob_id,1, .@label$;
	}
	return;
}

1@glast,1,1,0	script	#fenrir_left_vermilion	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	enablenpc instance_npcname("#fenrir_left_vermilion");
	initnpctimer;
	npctalk "芬里尔：他们是无穷无尽的。我绝对要完成这件事。", 'fenrir_4$;
	progressbar_npc "000000",10, 'fenrir_4$;
	end;
OnTimer2000:
	npctalk "芬里尔：无限遥远的君主与美丽的红色气息！！", 'fenrir_4$;
	end;
OnTimer4000:
	npctalk "芬里尔：你过去只会充满了虚无。", 'fenrir_4$;
	end;
OnTimer7000:
	npctalk "芬里尔：震憾的轴！在此时此地展现你的恩典！！！", 'fenrir_4$;
	end;
OnTimer10000:
	stopnpctimer;
	npctalk "芬里尔：路Vermilion！！！！", 'fenrir_4$;
	for ( .@i = 1; .@i < 9; .@i++ )
		specialeffect EF_LORD, AREA, instance_npcname("#effect_glast_event02_" + .@i);
	killmonster 'map_glast$, instance_npcname("#fenrir_left_final_1") + "::OnMobDead";
	donpcevent instance_npcname("#fenrir_left_final_1") + "::OnStart";
	disablenpc instance_npcname("#fenrir_left_vermilion");
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#fenrir_left_vermilion");
	end;
}

1@glast,1,1,0	script	#fenrir_left_final_1	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer2000:
	npctalk "芬里尔：好吧！我们战胜了一个巨人！", 'fenrir_4$;
	enablenpc 'sarah_10$;
	end;
OnTimer4000:
	npctalk "莎拉艾琳：不出所料, 芬里尔, 你并不是那么简单的!", 'sarah_10$;
	end;
OnTimer7000:
	npctalk "芬里尔：留下巴尔姆！离开这里吧！立刻！", 'fenrir_4$;
	end;
OnTimer9000:
	npctalk "莎拉艾琳：呵呵。我不能那样做！", 'sarah_10$;
	specialeffect EF_WL_TELEKINESIS_INTENSE, AREA, instance_npcname("#effect_glast_event02_bis");
	end;
OnTimer11000:
	npctalk "莎拉艾琳：如果你想要回这把剑！那就来克雷斯特汉姆中央城堡，我在那里等你！", 'sarah_10$;
	end;
OnTimer13000:
	specialeffect EF_FIREPILLARON2, AREA, 'sarah_10$;
	end;
OnTimer14000:
	disablenpc 'sarah_10$;
	end;
OnTimer16000:
	npctalk "芬里尔：我们必须快点赶上，并夺回那把剑！我使用团体空间传送咒语快速跟上！", 'fenrir_4$;
	end;
OnTimer19000:
	npctalk "芬里尔：‘咒语’！", 'fenrir_4$;
	end;
OnTimer20000:
	stopnpctimer;
	//for ( .@i = 1; .@i < 8; .@i++ )
	//	enablenpc instance_npcname("#glast_event_warp_" + .@i);	// warp to 105,369
	areawarp 'map_glast$,26,338,59,375, 'map_glast$,105,369;
	for ( .@i = 1; .@i < 9; .@i++ )
		disablenpc instance_npcname("#effect_glast_event02_" + .@i);
	disablenpc instance_npcname("#effect_glast_event02_bis");
	disablenpc instance_npcname("#fenrir_left_final_1");
	disablenpc 'fenrir_4$;
	enablenpc instance_npcname("#glast_event_13");
	enablenpc 'fenrir_5$;
	end;

OnMobDead:
	if (mobcount( 'map_glast$, instance_npcname("#fenrir_left_final_1") + "::OnMobDead" ) < 1) {
		donpcevent instance_npcname("#glast_event_9") + "::OnStop";
		donpcevent instance_npcname("#fenrir_left_vermilion") + "::OnStop";
		donpcevent instance_npcname("#fenrir_left_final_1") + "::OnStart";
	}
	end;
}

1@glast,44,357,8	duplicate(#effect_glast_event01_1)	芬里尔#glast_04	4_F_FENRIR
1@glast,49,363,4	duplicate(#effect_glast_event01_1)	巨人#semi1	4_GIGANTES_SMALL
1@glast,41,363,4	duplicate(#effect_glast_event01_1)	巨人#semi2	4_GIGANTES_SMALL
1@glast,46,364,4	duplicate(#effect_glast_event01_1)	莎拉艾琳#glast_10	4_F_SARAH

1@glast,52,345,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_1	CLEAR_NPC
1@glast,44,350,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_2	CLEAR_NPC
1@glast,35,360,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_3	CLEAR_NPC
1@glast,44,360,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_4	CLEAR_NPC
1@glast,52,360,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_5	CLEAR_NPC
1@glast,35,368,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_6	CLEAR_NPC
1@glast,53,368,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_7	CLEAR_NPC
1@glast,44,371,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_8	CLEAR_NPC
1@glast,44,366,4	duplicate(#effect_glast_event01_1)	#effect_glast_event02_bis	CLEAR_NPC

// Event top side
1@glast,133,365,0	script	#glast_event_13	HIDDEN_WARP_NPC,8,8,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_event_13");
	//for ( .@i = 1; .@i < 8; .@i++ )
	//	disablenpc instance_npcname("#glast_event_warp_" + .@i);
	setpcblock PCBLOCK_NPC, true;
	cutin "fenrir_a.bmp",2;
	sleep2 1000;
	npctalk "芬里尔：我需要去检索GLAST海姆城堡博德之剑。", 'fenrir_5$;
	sleep2 3000;
	npctalk "芬里尔：它可能很难独自, I would gratefully accept your help.", 'fenrir_5$;
	sleep2 3000;
	npctalk "芬里尔：因为没有足够的时间, we can't wait long.", 'fenrir_5$;
	sleep2 3000;
	npctalk "芬里尔：我会在GLAST海姆中央城堡等待约5分钟。", 'fenrir_5$;
	sleep2 3000;
	npctalk "芬里尔：因为它是危险的你不觉得有义务帮助我。如果我必须的话，我会自己尝试。", 'fenrir_5$;
	viewpoint 1,199,237,1,0xFF0000;
	sleep2 3000;
	npctalk "芬里尔：然后我会走在前面！弯曲!", 'fenrir_5$;
	cutin "",255;
	donpcevent instance_npcname("#glast_event_13") + "::OnBar";
	setpcblock PCBLOCK_NPC, false;
	end;
OnBar:
	progressbar_npc "000000",3, 'fenrir_5$;
	disablenpc 'fenrir_5$;
	donpcevent instance_npcname("#fenrir_boss_final_2") + "::OnStart";// timer (5 mins)
	enablenpc instance_npcname("#glast_move_03");
	enablenpc instance_npcname("#glast_event_15");
	enablenpc 'fenrir_6$;
	end;
}

1@glast,133,365,4	duplicate(#effect_glast_event01_1)	芬里尔#glast_05	4_F_FENRIR

// Skipped left side
1@glast,1,1,0	script	#fenrir_left_final_2	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#fenrir_left_final_2");
	initnpctimer;
	end;
OnTimer300000:
	'skip_left_part = 1;
	disablenpc instance_npcname("#glast_event_7");
	disablenpc 'fenrir_3$;
	mapannounce 'map_glast$, "芬里尔: Alright! We have defeated a 巨人!", bc_map,0xEBFF;
	end;
OnTimer302000:
	mapannounce 'map_glast$, "莎拉艾琳: As expected, 芬里尔, you do not yield easily!", bc_map,0xEBFF;
	end;
OnTimer305000:
	mapannounce 'map_glast$, "芬里尔: Get away from the Sentinel Breeze immediately!", bc_map,0xEBFF;
	end;
OnTimer307000:
	mapannounce 'map_glast$, "莎拉艾琳: Hehe.. I can't do that!", bc_map,0xEBFF;
	end;
OnTimer309000:
	mapannounce 'map_glast$, "莎拉艾琳: If you want it back come to Glast Heim Castle! I shall be waiting!", bc_map,0xEBFF;
	end;
OnTimer311000:
	mapannounce 'map_glast$, "芬里尔: I need to go to Glast Heim Castle to retrieve the sword of Baldur", bc_map,0xEBFF;
	end;
OnTimer313000:
	mapannounce 'map_glast$, "芬里尔: It may be difficult alone. If you are listening I will gratefully accept your help.", bc_map,0xEBFF;
	end;
OnTimer315000:
	mapannounce 'map_glast$, "芬里尔: Since there isn't enough time, we can't wait long.", bc_map,0xEBFF;
	end;
OnTimer317000:
	mapannounce 'map_glast$, "芬里尔: I will wait in the central castle of Glast Heim for about 5 minutes.", bc_map,0xEBFF;
	end;
OnTimer319000:
	stopnpctimer;
	mapannounce 'map_glast$, "芬里尔: Since it is dangerous you do not have to feel obligated to help me. I will try by myself if I have to.", bc_map,0xEBFF;
	enablenpc instance_npcname("#glast_move_03");
	enablenpc 'fenrir_6$;
	enablenpc instance_npcname("#glast_event_15");
	disablenpc instance_npcname("#fenrir_left_final_2");
	donpcevent instance_npcname("#fenrir_boss_final_2") + "::OnStart";// timer (5 mins)
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#fenrir_left_final_2");
	end;
}

// Stairs final room
1@glast,200,268,0	script	#glast_event_15	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_move_03");
	disablenpc instance_npcname("#glast_event_15");
	donpcevent instance_npcname("#fenrir_boss_final_2") + "::OnStop";
	setpcblock PCBLOCK_NPC, true;
	sleep2 1000;
	npctalk "芬里尔：我在等待。谢谢你没有迟到。", 'fenrir_6$;
	sleep2 3000;
	npctalk "芬里尔：让我们爬上城堡的检索前哨的微风！莎拉会等的！", 'fenrir_6$;
	sleep2 3000;
	disablenpc 'fenrir_6$;
	enablenpc instance_npcname("#glast_move_03a");
	enablenpc instance_npcname("#glast_event_18");
	enablenpc instance_npcname("莎拉艾琳#glast_17");
	enablenpc 'fenrir_7$;
	setpcblock PCBLOCK_NPC, false;
	end;
}

1@glast,200,268,4	duplicate(#effect_glast_event01_1)	芬里尔#glast_06	4_F_FENRIR

// Final room
1@glast,199,336,0	script	#glast_event_18	HIDDEN_WARP_NPC,10,10,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_event_18");
	setpcblock PCBLOCK_NPC, true;
	cutin "sarah_hero3.bmp",0;
	sleep2 1000;
	npctalk "莎拉艾琳：好像永远等你~芬里尔。", 'sarah_17$;
	sleep2 3000;
	cutin "fenrir_b.bmp",2;
	npctalk "芬里尔：不需要无用的话, hand over the Sentinel Breeze now!", 'fenrir_7$;
	sleep2 3000;
	cutin "sarah_hero3.bmp",0;
	npctalk "莎拉艾琳：如果可以的话试试看。", 'sarah_17$;
	sleep2 3000;
	cutin "fenrir_b.bmp",2;
	npctalk "芬里尔：莎拉可能要面对孤独太难。我必须面对她！", 'fenrir_7$;
	sleep2 3000;
	npctalk "芬里尔：纯白色的束缚, eternal prison, make a deal with me now!", 'fenrir_7$;
	donpcevent instance_npcname("#glast_event_18") + "::OnBar";
	sleep2 3000;
	npctalk "芬里尔：白imprison！！", 'fenrir_7$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	sleep2 3000;
	npctalk "莎拉艾琳：hurk的…他..", 'sarah_17$;
	npctalk "芬里尔：我会削弱莎拉的白色的禁锢，面对她而她束缚,", 'fenrir_7$;
	sleep2 3000;
	npctalk "芬里尔：你照顾她召唤的怪物。我指望着你。", 'fenrir_7$;
	sleep2 3000;
	unittalk getcharid(3), "" + strcharinfo(0) + " : Will do. Powerful enemies make my blood boil.";
	sleep2 6000;
	npctalk "芬里尔：这是一个战略，立即进攻并完成了莎拉当她在临界条件。记得, our opponent is strong.", 'fenrir_7$;
	sleep2 3000;
	cutin "sarah_hero3_2.bmp",0;
	npctalk "莎拉艾琳：那么你的计划是把我和这种把戏绑在一起？", 'sarah_17$;
	sleep2 3000;
	npctalk "莎拉艾琳：此外，你还借用了这些新手冒险家的帮助…", 'sarah_17$;
	sleep2 3000;
	npctalk "莎拉艾琳：你需要从这里消失，连同那破旧的刀刃！！", 'sarah_17$;
	sleep2 3000;
	cutin "",255;
	hideonnpc 'sarah_17$;
	donpcevent instance_npcname("#glast_event_19") + "::OnEvent";
	monster 'map_glast$,200,336, "莎拉艾琳#1",3190,1, instance_npcname("#fenrir_boss_final_1") + "::OnBossDead";	// MM_SARAH
	'boss_id = $@mobid[0];
	setpcblock PCBLOCK_NPC, false;
	end;
OnBar:
	progressbar_npc "000000",3, 'fenrir_7$;
	end;
}

1@glast,200,336,4	duplicate(#effect_glast_event01_1)	莎拉艾琳#glast_17	4_F_SARAH

1@glast,197,330,8	script	芬里尔#glast_07	4_F_FENRIR,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer1000:
	specialeffect EF_TETRACASTING;
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}

1@glast,212,350,0	script	#glast_event_19	HIDDEN_WARP_NPC,{
	end;
OnEvent:
	enablenpc instance_npcname("#glast_event_19");
	enablenpc instance_npcname("#fenrir_boss_final_1");
	initnpctimer;
	specialeffect EF_PRIMECHARGE4, AREA, 'fenrir_7$;
	npctalk "芬里尔：致命的毒药, Dark clouds, appear before me and show a most painful death!", 'fenrir_7$;
	monster 'map_glast$,190,341, "Large 巨人#a1", 3194,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN1
	monster 'map_glast$,214,331, "Large 巨人#b2", 3195,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN2
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer1000:
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer10000:
	callsub S_Hit,1;
	npctalk "芬里尔：去！！！！！！！云杀！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！", 'fenrir_7$;
	specialeffect EF_POISONSMOKE, AREA, 'sarah_17$;
	for ( .@i = 1; .@i <= 8; .@i++ )
		specialeffect EF_POISONSMOKE, AREA, instance_npcname("#effect_glast_event03_" + .@i);
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer10500:
	unittalk 'boss_id, "莎拉艾琳: That was sudden Fenrir. Is that all you've got?";
	end;
OnTimer11000:
	specialeffect EF_POISONSMOKE2, AREA, 'sarah_17$;
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_1");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_2");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_3");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_4");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_5");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_6");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_7");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_8");
	end;
OnTimer13000:
	npctalk "芬里尔：致命的毒药, Dark clouds, appear before me and show a most painful death!", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE4, AREA, 'fenrir_7$;
	specialeffect EF_POISONSMOKE2, AREA, 'sarah_17$;
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_1");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_2");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_3");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_4");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_5");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_6");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_7");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_8");
	end;
OnTimer20000:
	callsub S_Hit,2;
	npctalk "芬里尔：去！！！！！！！云杀！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！！", 'fenrir_7$;
	specialeffect EF_POISONSMOKE, AREA, 'sarah_17$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	for ( .@i = 1; .@i <= 8; .@i++ )
		specialeffect EF_POISONSMOKE, AREA, instance_npcname("#effect_glast_event03_" + .@i);
	progressbar_npc "000000",30, 'fenrir_7$;
	end;
OnTimer20500:
	unittalk 'boss_id, "莎拉艾琳: Try to entertain me a little more!!";
	end;
OnTimer21000:
	specialeffect EF_POISONSMOKE2, AREA, 'sarah_17$;
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_1");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_2");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_3");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_4");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_5");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_6");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_7");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_8");
	end;
OnTimer23000:
	npctalk "芬里尔：我告诉你谁控制火, I Fenrith, desire a fire that does not go out...", 'fenrir_7$;
	specialeffect EF_BEGINSPELL_YB, AREA, 'fenrir_7$;
	specialeffect EF_POISONSMOKE2, AREA, 'sarah_17$;
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_1");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_2");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_3");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_4");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_5");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_6");
	specialeffect EF_POISONSMOKE2, AREA, instance_npcname("#effect_glast_event03_7");
	specialeffect EF_ENCHANTPOISON, AREA, instance_npcname("#effect_glast_event03_8");
	end;
OnTimer36000:
	npctalk "芬里尔：不断燃烧的大火, a fire that can scorch even the soul!! Appear before me now!!", 'fenrir_7$;
	specialeffect EF_BEGINSPELL_YB, AREA, 'fenrir_7$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer50000:
	callsub S_Hit,3;
	npctalk "芬里尔：出现！火焰徽章！！！！", 'fenrir_7$;
	specialeffect EF_DEMONICFIRE, AREA, 'sarah_17$;
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer50500:
	unittalk 'boss_id, "莎拉艾琳: Oh... not bad? But not good enough!!";
	end;
OnTimer51000:
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer53000:
	npctalk "芬里尔：惊天动地的力量的根源！恐惧地球的力量！！！", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE4, AREA, 'fenrir_7$;
	end;
OnTimer60000:
	callsub S_Hit,4;
	npctalk "芬里尔：出去！天哪！！！", 'fenrir_7$;
	specialeffect EF_HEAVENSDRIVE, AREA, 'sarah_17$;
	monster 'map_glast$,190,341, "Large 巨人#a1", 3194,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN1
	monster 'map_glast$,214,331, "Large 巨人#b2", 3195,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN2
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer60500:
	unittalk 'boss_id, "莎拉艾琳: Why don't you stop your childish magic, Fenrir?";
	end;
OnTimer63000:
	specialeffect EF_PRIMECHARGE4, AREA, 'fenrir_7$;
	npctalk "芬里尔：惊天动地的力量的根源！恐惧地球的力量！！！", 'fenrir_7$;
	end;
OnTimer70000:
	callsub S_Hit,5;
	npctalk "芬里尔：出去！天哪！！！", 'fenrir_7$;
	specialeffect EF_HEAVENSDRIVE, AREA, 'sarah_17$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	progressbar_npc "000000",30, 'fenrir_7$;
	end;
OnTimer70500:
	unittalk 'boss_id, "莎拉艾琳: Do you think that is enough to beat me?";
	end;
OnTimer73000:
	npctalk "芬里尔：北方的风无情的寒风,", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE2, AREA, 'fenrir_7$;
	end;
OnTimer85000:
	npctalk "芬里尔：来像寂静的黎明的薄雾和冻结一切！！", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE2, AREA, 'fenrir_7$;
	end;
OnTimer100000:
	callsub S_Hit,6;
	npctalk "芬里尔：出现！！！Frost Misty！！！！！", 'fenrir_7$;
	specialeffect EF_FROSTMYSTY, AREA, 'sarah_17$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	specialeffect EF_FROSTMYSTY, AREA, 'sarah_17$;
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer100500:
	unittalk 'boss_id, "莎拉艾琳: Not bad but.. not even close!";
	end;
OnTimer103000:
	npctalk "芬里尔：权力的疑问, a powerful force, the power of deeply hidden secrets in the mind!!!", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE, AREA, 'fenrir_7$;
	end;
OnTimer110000:
	callsub S_Hit,7;
	npctalk "芬里尔：现在！！！心灵的波！！！！", 'fenrir_7$;
	specialeffect EF_SPR_PLANT10, AREA, 'sarah_17$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer110500:
	unittalk 'boss_id, "莎拉艾琳: Ugh... that is no use Fenrir!";
	end;
OnTimer113000:
	npctalk "芬里尔：权力的疑问, a powerful force, the power of deeply hidden secrets in the mind!!!", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE, AREA, 'fenrir_7$;
	end;
OnTimer120000:
	callsub S_Hit,8;
	npctalk "芬里尔：现在！！！心灵的波！！！！", 'fenrir_7$;
	specialeffect EF_SPR_PLANT10, AREA, 'sarah_17$;
	monster 'map_glast$,190,341, "Large 巨人#a1", 3194,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN1
	monster 'map_glast$,214,331, "Large 巨人#b2", 3195,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN2
	progressbar_npc "000000",30, 'fenrir_7$;
	end;
OnTimer120500:
	unittalk 'boss_id, "莎拉艾琳: Hurk.. such an annoying magic bombardment!!";
	end;
OnTimer123000:
	npctalk "芬里尔：我告诉你谁控制火, I Fenrith, desire a fire that does not go out...", 'fenrir_7$;
	specialeffect EF_BEGINSPELL_YB, AREA, 'fenrir_7$;
	end;
OnTimer135000:
	npctalk "芬里尔：不断燃烧的大火, a fire that can scorch even the soul!! Appear before me now!!", 'fenrir_7$;
	specialeffect EF_BEGINSPELL_YB, AREA, 'fenrir_7$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer150000:
	callsub S_Hit,9;
	npctalk "芬里尔：出现！火焰徽章！！！！", 'fenrir_7$;
	specialeffect EF_DEMONICFIRE, AREA, 'sarah_17$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer150500:
	unittalk 'boss_id, "莎拉艾琳: I am 莎拉艾琳! I will not fall so easily!!!";
	end;
OnTimer153000:
	npctalk "芬里尔威胁：愤怒的云, thunderbolt that may split the earth... Fall before me now!!", 'fenrir_7$;
	specialeffect EF_DQ9_CHARGE3, AREA, 'fenrir_7$;
	end;
OnTimer160000:
	callsub S_Hit,10;
	npctalk "芬里尔：来吧！雷雨！！！！！", 'fenrir_7$;
	specialeffect EF_THUNDERSTORM2, AREA, 'sarah_17$;
	unittalk 'boss_id, "莎拉艾琳: Ugh...you are.. stronger than I predicted Fenrir...";
	progressbar_npc "000000",10, 'fenrir_7$;
	end;
OnTimer160500:
	unittalk 'boss_id, "莎拉艾琳: Ugh...you are.. stronger than I predicted Fenrir...";
	end;
OnTimer163000:
	npctalk "芬里尔威胁：愤怒的云, thunderbolt that may split the earth... Fall before me now!!", 'fenrir_7$;
	specialeffect EF_DQ9_CHARGE3, AREA, 'fenrir_7$;
	end;
OnTimer170000:
	callsub S_Hit,11;
	npctalk "芬里尔：来吧！雷雨！！！！！", 'fenrir_7$;
	specialeffect EF_THUNDERSTORM2, AREA, 'sarah_17$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	progressbar_npc "000000",30, 'fenrir_7$;
	end;
OnTimer170500:
	unittalk 'boss_id, "莎拉艾琳: Argh!!!!!! Ahhh!!!!!";
	end;
OnTimer173000:
	npctalk "芬里尔：北方的风无情的寒风,", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE2, AREA, 'fenrir_7$;
	end;
OnTimer180000:
	monster 'map_glast$,190,341, "Large 巨人#a1", 3194,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN1
	monster 'map_glast$,185,331, "Large 巨人#a2", 3194,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN1
	monster 'map_glast$,209,341, "Large 巨人#b1", 3195,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN2
	monster 'map_glast$,214,331, "Large 巨人#b2", 3195,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN2
	end;
OnTimer185000:
	npctalk "芬里尔：来像寂静的黎明的薄雾和冻结一切！！", 'fenrir_7$;
	specialeffect EF_PRIMECHARGE2, AREA, 'fenrir_7$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer200000:
	callsub S_Hit,12;
	npctalk "芬里尔：出现！！！Frost Misty！！！！！", 'fenrir_7$;
	specialeffect EF_FROSTMYSTY, AREA, 'sarah_17$;
	donpcevent 'fenrir_7$ + "::OnStart";
	progressbar_npc "000000",50, 'fenrir_7$;
	end;
OnTimer200500:
	unittalk 'boss_id, "莎拉艾琳: Argh!!! I will kill you all!!!!!!";
	specialeffect EF_FROSTMYSTY, AREA, 'sarah_17$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer203000:
	npctalk "芬里尔：天堂之剑刺破天空", 'fenrir_7$;
	specialeffect EF_BLACKBODY, AREA, 'fenrir_7$;
	specialeffect EF_RECOGNIZED, AREA, 'fenrir_7$;
	end;
OnTimer216000:
	npctalk "芬里尔：我想借你的力量一瞬间！！", 'fenrir_7$;
	end;
OnTimer230000:
	npctalk "芬里尔：让掉在这儿你极端的力量能够撕裂甚至真空！！", 'fenrir_7$;
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer239000:
	monster 'map_glast$,190,341, "Large 巨人#a1", 3194,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN1
	monster 'map_glast$,185,331, "Large 巨人#a2", 3194,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN1
	monster 'map_glast$,209,341, "Large 巨人#b1", 3195,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN2
	monster 'map_glast$,214,331, "Large 巨人#b2", 3195,1, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";// MM_L_GIGAN2
	end;
OnTimer240000:
	npctalk "芬里尔：剑，可以控制生命和死亡, and pierce space time!!!", 'fenrir_7$;
	end;
OnTimer250000:
	npctalk "芬里尔baretis矛：出来！！！！！！", 'fenrir_7$;
	specialeffect EF_PRESSURE3, AREA, 'sarah_17$;
	for ( .@i = 1; .@i <= 8; .@i++ )
		specialeffect EF_PRESSURE3, AREA, instance_npcname("#effect_glast_event03_" + .@i);
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	end;
OnTimer253000:
	setunitdata 'boss_id, UMOB_HP, 100;
	specialeffect EF_PRESSURE3, AREA, 'sarah_17$;
	for ( .@i = 1; .@i <= 8; .@i++ )
		specialeffect EF_PRESSURE3, AREA, instance_npcname("#effect_glast_event03_" + .@i);
	npctalk "芬里尔：现在！我把莎拉置于危急状态！你必须结束她！迅速攻击她！莎拉很强壮！", 'fenrir_7$;
	mapannounce 'map_glast$, "芬里尔: Now! I put Sarah in critical condition! You have to finish her off!! Attack her quickly! Sarah is strong!", bc_map,0xEBFF;
	npctalk "莎拉艾琳：hurk…..不可能发生！！！我不能输！！！！！", 'sarah_17$;
	end;
OnTimer263000:
	npctalk "芬里尔：因为你可能无法完成她独自离开, I will prepare one final spell!!", 'fenrir_7$;
	progressbar_npc "000000",50, 'fenrir_7$;
	end;
OnTimer273000:
	specialeffect EF_BOTTOM_BASILICA2, AREA, 'sarah_17$;
	npctalk "芬里尔：天堂之剑刺破天空", 'fenrir_7$;
	specialeffect EF_RECOGNIZED, AREA, 'fenrir_7$;
	specialeffect EF_BLACKBODY, AREA, 'fenrir_7$;
	end;
OnTimer283000:
	npctalk "芬里尔：我想借你的力量一瞬间！！", 'fenrir_7$;
	end;
OnTimer293000:
	npctalk "芬里尔：让掉在这儿你极端的力量能够撕裂甚至真空！！";
	end;
OnTimer303000:
	npctalk "芬里尔：剑，可以控制生命和死亡, and pierce space time!!!";
	end;
OnTimer313000:
	npctalk "芬里尔baretis矛：出来！！！！！！";
	specialeffect EF_PRESSURE3, AREA, 'sarah_17$;
	for ( .@i = 1; .@i <= 8; .@i++ )
		specialeffect EF_PRESSURE3, AREA, instance_npcname("#effect_glast_event03_" + .@i);
	donpcevent 'fenrir_7$ + "::OnStop";
	end;
OnTimer316000:
	specialeffect EF_PRESSURE3, AREA, 'sarah_17$;
	for ( .@i = 1; .@i <= 8; .@i++ )
		specialeffect EF_PRESSURE3, AREA, instance_npcname("#effect_glast_event03_" + .@i);
	killmonster 'map_glast$, instance_npcname("#fenrir_boss_final_1") + "::OnBossDead";
	donpcevent instance_npcname("#fenrir_boss_final_1") + "::OnBossDead";
	end;

OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#glast_event_19");
	end;

S_Hit:
	getunitdata 'boss_id, .@data;
	.@hp = .@data[UMOB_MAXHP] - ((.@data[UMOB_MAXHP] * getarg(0)) / 13);
	setunitdata 'boss_id, UMOB_HP, .@hp;
	return;
}

1@glast,1,1,0	script	#fenrir_boss_final_1	HIDDEN_WARP_NPC,{
	end;
OnBossDead:
	donpcevent instance_npcname("#glast_event_19") + "::OnStop";
	initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_glast$, instance_npcname("#fenrir_boss_final_1") + "::OnMobDead";
	end;
OnTimer6000:
	disablenpc 'sarah_17$;
	disablenpc 'fenrir_7$;
	enablenpc 'fenrir_8$;
	enablenpc 'sarah_20$;
	end;
OnTimer9000:
	npctalk "芬里尔：前哨的微风！我手里拿着它！", 'fenrir_8$;
	end;
OnTimer12000:
	npctalk "莎拉艾琳：hurk的…", 'sarah_20$;
	end;
OnTimer15000:
	npctalk "芬里尔：我能找到Baldur这样的！", 'fenrir_8$;
	end;
OnTimer18000:
	npctalk "莎拉艾琳：还没有！！", 'sarah_20$;
	progressbar_npc "000000",2, 'sarah_20$;
	end;
OnTimer21000:
	specialeffect EF_FIREPILLARON, AREA, 'fenrir_8$;
	disablenpc 'fenrir_8$;
	enablenpc 'fenrir_9$;
	end;
OnTimer24000:
	npctalk "莎拉艾琳：呵呵…你像老鼠一样躲避得很好。", 'sarah_20$;
	end;
OnTimer27000:
	npctalk "莎拉艾琳：但是, I cannot let you leave with that.", 'sarah_20$;
	end;
OnTimer30000:
	npctalk "莎拉艾琳：我要炸掉整个城堡！yaah！", 'sarah_20$;
	progressbar_npc "000000",15, 'sarah_20$;
	end;
OnTimer33000:
	npctalk "芬里尔：她打算这个崩溃的地方！我必须进行大规模的隐形传态！！！", 'fenrir_9$;
	progressbar_npc "000000",6, 'fenrir_9$;
	end;
OnTimer36000:
	npctalk "芬里尔：告别女神！我将接受哨兵的微风！这么久!", 'fenrir_9$;
	end;
OnTimer39000:
	for ( .@i = 1; .@i <= 8; .@i++ )
		specialeffect EF_LORD, AREA, instance_npcname("#effect_glast_event03_" + .@i);
	disablenpc 'fenrir_9$;
	end;
OnTimer40000:
	stopnpctimer;
	enablenpc instance_npcname("#glast_event_24");
	enablenpc instance_npcname("芬里尔#glast_10");
	//for ( .@i = 11; .@i <= 16; .@i++ )
	//	enablenpc instance_npcname("#glast_event_warp_" + .@i);// warp to 197,221
	areawarp 'map_glast$,181,326,218,352, 'map_glast$,197,221;
	for ( .@i = 1; .@i <= 8; .@i++ )
		disablenpc instance_npcname("#effect_glast_event03_" + .@i);
	disablenpc 'sarah_20$;
	disablenpc instance_npcname("#fenrir_boss_final_1");
	end;

OnMobDead:
	end;
}

1@glast,199,348,8	duplicate(#effect_glast_event01_1)	芬里尔#glast_08	4_F_FENRIR
1@glast,201,346,4	duplicate(#effect_glast_event01_1)	芬里尔#glast_09	4_F_FENRIR
1@glast,197,339,8	duplicate(#effect_glast_event01_1)	莎拉艾琳#glast_20	4_F_SARAH

1@glast,201,336,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_1	CLEAR_NPC
1@glast,201,337,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_2	CLEAR_NPC
1@glast,200,337,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_3	CLEAR_NPC
1@glast,199,337,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_4	CLEAR_NPC
1@glast,199,336,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_5	CLEAR_NPC
1@glast,199,335,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_6	CLEAR_NPC
1@glast,200,335,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_7	CLEAR_NPC
1@glast,201,335,4	duplicate(#effect_glast_event01_1)	#effect_glast_event03_8	CLEAR_NPC

// Outside final room
1@glast,200,224,0	script	#glast_event_24	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch:
	disablenpc instance_npcname("#glast_event_24");
	setpcblock PCBLOCK_NPC, true;
	sleep2 1500;
	cutin "fenrir_b.bmp",2;
	npctalk "芬里尔：呵呵..你确实很有技巧，但我认为你低估了我。, Valkyrie.", 'fenrir_10$;
	sleep2 3000;
	mapannounce 'map_glast$, "莎拉艾琳: You rather entertain me Fenrith...", bc_map,0xEBFF;
	sleep2 3000;
	mapannounce 'map_glast$, "莎拉艾琳: Did you think you can escape that easily?", bc_map,0xEBFF;
	sleep2 3000;
	mapannounce 'map_glast$, "莎拉艾琳: I shall turn Glast Heim into hell!!", bc_map,0xEBFF;
	sleep2 3000;
	npctalk "芬里尔：如果我们不要逃避，我们将遭受可怕的命运被莎拉的魔法和怪兽！", 'fenrir_10$;
	sleep2 3000;
	npctalk "芬里尔：咱们在GLAST海姆入口，我们第一次见面的地方见面！如果我们逃得远远的，它应该是安全的！", 'fenrir_10$;
	sleep2 3000;
	npctalk "芬里尔：没有时间！快点！", 'fenrir_10$;
	sleep2 3000;
	cutin "",255;
	disablenpc 'fenrir_10$;
	donpcevent instance_npcname("#sarah_wrath") + "::OnStart";
	setpcblock PCBLOCK_NPC, false;
	end;
}

1@glast,200,224,4	duplicate(#effect_glast_event01_1)	芬里尔#glast_10	4_F_FENRIR

// Skipped final room
1@glast,1,1,0	script	#fenrir_boss_final_2	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#fenrir_boss_final_2");
	initnpctimer;
	end;
OnTimer300000:
	disablenpc instance_npcname("#glast_move_03");
	disablenpc instance_npcname("#glast_event_15");
	disablenpc 'fenrir_6$;
	end;
OnTimer305000:
	mapannounce 'map_glast$, "芬里尔: Sentinel Breeze! I have it in my hand!", bc_map,0xEBFF;
	end;
OnTimer308000:
	mapannounce 'map_glast$, "莎拉艾琳: Hurk....", bc_map,0xEBFF;
	end;
OnTimer311000:
	mapannounce 'map_glast$, "芬里尔: Hehe.. You are certainly skilled but I think you have underestimated me, Valkyrie.", bc_map,0xEBFF;
	end;
OnTimer314000:
	mapannounce 'map_glast$, "莎拉艾琳: You rather entertain me Fenrith...", bc_map,0xEBFF;
	end;
OnTimer317000:
	mapannounce 'map_glast$, "莎拉艾琳: Did you think you can escape that easily?", bc_map,0xEBFF;
	end;
OnTimer320000:
	mapannounce 'map_glast$, "莎拉艾琳: I shall turn Glast Heim into hell!!", bc_map,0xEBFF;
	end;
OnTimer323000:
	mapannounce 'map_glast$, "芬里尔: If we don't escape now we will suffer a gruesome fate by Sarah's monsters and magic!", bc_map,0xEBFF;
	end;
OnTimer326000:
	mapannounce 'map_glast$, "芬里尔: Let's meet at Glast Heim entrance where we first met! If we flee that far it should be safe!", bc_map,0xEBFF;
	end;
OnTimer329000:
	stopnpctimer;
	mapannounce 'map_glast$, "芬里尔: There is no time! Hurry!", bc_map,0xEBFF;
	donpcevent instance_npcname("#sarah_wrath") + "::OnStart";
	end;
OnStop:
	stopnpctimer;
	disablenpc instance_npcname("#fenrir_boss_final_2");
	end;
}

// Bombs
1@glast,1,1,0	script	#sarah_wrath	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#sarah_wrath");
	enablenpc instance_npcname("#glast_move_04");
	enablenpc 'fenrir_11$;
	enablenpc 'sarah_ego$;
	hideonnpc 'sarah_ego$;
	'back_to_entrance = 1;
	enablenpc instance_npcname("#glast_move_01");
	hideonnpc instance_npcname("#glast_move_01");
	enablenpc instance_npcname("#glast_move_05");
	initnpctimer;
	end;
OnTimer10000:
	mapannounce 'map_glast$, "莎拉艾琳: Absurd little thing, you think you can escape that easily?", bc_map,0xEBFF;
	callsub S_Spawn,0;
	end;
OnTimer50000:
	mapannounce 'map_glast$, "莎拉艾琳: My minions will make sure to kill you!!!", bc_map,0xEBFF;
	callsub S_Spawn,1;
	end;
OnTimer80000:
	mapannounce 'map_glast$, "莎拉艾琳: I cannot let you escape!!", bc_map,0xEBFF;
	callsub S_Spawn,2;
	end;
OnTimer110000:
	mapannounce 'map_glast$, "莎拉艾琳: 芬里尔.... Did you think I couldn't track you because you fled?", bc_map,0xEBFF;
	callsub S_Spawn,3;
	end;
OnTimer150000:
	mapannounce 'map_glast$, "莎拉艾琳: Absurd little thing, you think you can escape that easily?", bc_map,0xEBFF;
	callsub S_Spawn,4;
	end;
OnTimer300000:
	stopnpctimer;
	mapannounce 'map_glast$, "莎拉艾琳: Bwahahahahahaha!! Glast Heim is being filled with my wrath!!", bc_map,0xEBFF;
	for ( .@i = 1; .@i <= 14; .@i++ )
		enablenpc instance_npcname("#glast_bomb_" + (.@i < 10 ? ("0"+.@i) : .@i));
	end;
OnStop:
	stopnpctimer;
	end;

S_Spawn:
	// inaccurate
	switch( getarg(0) ) {
	case 0:
		monster 'map_glast$,204,52,"Large 巨人#aa1",3194,1;
		monster 'map_glast$,201,55,"Large 巨人#aa1",3194,1;
		monster 'map_glast$,196,53,"Large 巨人#aa1",3194,1;
		monster 'map_glast$,205,80,"Large 巨人#aa2",3195,1;
		monster 'map_glast$,210,77,"Large 巨人#aa2",3195,1;
		monster 'map_glast$,205,73,"Large 巨人#aa2",3195,1;
		monster 'map_glast$,236,96,"Large 巨人#aa5",3194,1;
		monster 'map_glast$,234,96,"Large 巨人#aa5",3194,1;
		monster 'map_glast$,234,96,"Large 巨人#aa5",3194,1;
		monster 'map_glast$,185,79,"Large 巨人#bb3",3194,1;
		monster 'map_glast$,189,78,"Large 巨人#bb3",3194,1;
		monster 'map_glast$,194,85,"Large 巨人#bb3",3194,1;
		monster 'map_glast$,168,113,"Large 巨人#bb4",3195,1;
		monster 'map_glast$,174,122,"Large 巨人#bb4",3195,1;
		break;
	case 1:
		monster 'map_glast$,131,182,"Large 巨人#bb11",3194,1;
		monster 'map_glast$,133,174,"Large 巨人#bb11",3194,1;
		monster 'map_glast$,134,187,"Large 巨人#bb11",3194,1;
		monster 'map_glast$,202,196,"Large 巨人#aa9",3194,1;
		monster 'map_glast$,196,194,"Large 巨人#aa9",3194,1;
		monster 'map_glast$,199,190,"Large 巨人#aa9",3194,1;
		monster 'map_glast$,233,198,"Large 巨人#bb8",3195,1;
		monster 'map_glast$,231,193,"Large 巨人#bb8",3195,1;
		monster 'map_glast$,232,196,"Large 巨人#bb8",3195,1;
		monster 'map_glast$,258,196,"Large 巨人#bb7",3194,1;
		monster 'map_glast$,265,187,"Large 巨人#bb7",3194,1;
		monster 'map_glast$,263,195,"Large 巨人#bb7",3194,1;
		monster 'map_glast$,271,106,"Large 巨人#aa6",3195,1;
		monster 'map_glast$,266,107,"Large 巨人#aa6",3195,1;
		monster 'map_glast$,272,109,"Large 巨人#aa6",3195,1;
		break;
	case 2:
		monster 'map_glast$,127,204,"Large 巨人#bb12",3195,1;
		monster 'map_glast$,130,205,"Large 巨人#bb12",3195,1;
		monster 'map_glast$,130,213,"Large 巨人#bb12",3195,1;
		monster 'map_glast$,139,243,"Large 巨人#bb19",3194,1;
		monster 'map_glast$,143,241,"Large 巨人#bb19",3194,1;
		monster 'map_glast$,146,234,"Large 巨人#bb19",3194,1;
		monster 'map_glast$,199,218,"Large 巨人#aa14",3195,1;
		monster 'map_glast$,201,219,"Large 巨人#aa14",3195,1;
		monster 'map_glast$,193,212,"Large 巨人#aa14",3195,1;
		monster 'map_glast$,261,251,"Large 巨人#aa18",3195,1;
		monster 'map_glast$,257,254,"Large 巨人#aa18",3195,1;
		monster 'map_glast$,257,250,"Large 巨人#aa18",3195,1;
		monster 'map_glast$,146,45,"Large 巨人#bb35",3194,1;
		monster 'map_glast$,145,49,"Large 巨人#bb35",3194,1;
		monster 'map_glast$,144,44,"Large 巨人#bb35",3194,1;
		break;
	case 3:
		monster 'map_glast$,67,259,"Large 巨人#aa29",3194,1;
		monster 'map_glast$,68,261,"Large 巨人#aa29",3194,1;
		monster 'map_glast$,61,260,"Large 巨人#aa29",3194,1;
		monster 'map_glast$,64,208,"Large 巨人#aa30",3195,1;
		monster 'map_glast$,67,210,"Large 巨人#aa30",3195,1;
		monster 'map_glast$,63,204,"Large 巨人#aa30",3195,1;
		monster 'map_glast$,87,57,"Large 巨人#aa33",3194,1;
		monster 'map_glast$,81,64,"Large 巨人#aa33",3194,1;
		monster 'map_glast$,83,58,"Large 巨人#aa33",3194,1;
		monster 'map_glast$,107,47,"Large 巨人#aa34",3195,1;
		monster 'map_glast$,105,45,"Large 巨人#aa34",3195,1;
		monster 'map_glast$,102,48,"Large 巨人#aa34",3195,1;
		monster 'map_glast$,80,152,"Large 巨人#bb31",3194,1;
		monster 'map_glast$,82,149,"Large 巨人#bb31",3194,1;
		monster 'map_glast$,75,156,"Large 巨人#bb31",3194,1;
		monster 'map_glast$,85,106,"Large 巨人#bb32",3195,1;
		monster 'map_glast$,82,111,"Large 巨人#bb32",3195,1;
		monster 'map_glast$,78,112,"Large 巨人#bb32",3195,1;
		monster 'map_glast$,55,149,"Large 巨人#bb36",3195,1;
		monster 'map_glast$,56,143,"Large 巨人#bb36",3195,1;
		monster 'map_glast$,49,142,"Large 巨人#bb36",3195,1;
		break;
	case 4:
		monster 'map_glast$,262,215,"Large 巨人#aa17",3194,1;
		monster 'map_glast$,266,212,"Large 巨人#aa17",3194,1;
		monster 'map_glast$,260,219,"Large 巨人#aa17",3194,1;
		monster 'map_glast$,253,47,"Large 巨人#aa26",3195,1;
		monster 'map_glast$,263,42,"Large 巨人#aa26",3195,1;
		monster 'map_glast$,255,39,"Large 巨人#aa26",3195,1;
		monster 'map_glast$,202,219,"Large 巨人#bb15",3194,1;
		monster 'map_glast$,201,222,"Large 巨人#bb15",3194,1;
		monster 'map_glast$,201,220,"Large 巨人#bb15",3194,1;
		monster 'map_glast$,231,210,"Large 巨人#bb16",3195,1;
		monster 'map_glast$,345,117,"Large 巨人#bb27",3194,1;
		monster 'map_glast$,341,115,"Large 巨人#bb27",3194,1;
		monster 'map_glast$,351,121,"Large 巨人#bb27",3194,1;
		monster 'map_glast$,346,252,"Large 巨人#bb28",3195,1;
		monster 'map_glast$,351,256,"Large 巨人#bb28",3195,1;
		monster 'map_glast$,345,254,"Large 巨人#bb28",3195,1;
		break;
	}
	end;
}

1@glast,290,43,0	script	#glast_bomb_01	HIDDEN_WARP_NPC,10,10,{
	end;
OnTouch:
	unitkill 0;
	mapannounce 'map_glast$, "莎拉艾琳: Bwahahahahahaha!! Glast Heim is being filled with my wrath!!", bc_map,0xEBFF;
	specialeffect2 EF_LORD;
	sleep2 1000;
	specialeffect2 EF_LORD;
	end;
}

1@glast,234,47,0	duplicate(#glast_bomb_01)	#glast_bomb_02	HIDDEN_WARP_NPC,10,10
1@glast,265,175,0	duplicate(#glast_bomb_01)	#glast_bomb_03	HIDDEN_WARP_NPC,10,10
1@glast,135,176,0	duplicate(#glast_bomb_01)	#glast_bomb_04	HIDDEN_WARP_NPC,10,10
1@glast,146,238,0	duplicate(#glast_bomb_01)	#glast_bomb_05	HIDDEN_WARP_NPC,10,10
1@glast,165,212,0	duplicate(#glast_bomb_01)	#glast_bomb_06	HIDDEN_WARP_NPC,10,10
1@glast,232,212,0	duplicate(#glast_bomb_01)	#glast_bomb_07	HIDDEN_WARP_NPC,10,10
1@glast,253,241,0	duplicate(#glast_bomb_01)	#glast_bomb_08	HIDDEN_WARP_NPC,10,10
1@glast,268,290,0	duplicate(#glast_bomb_01)	#glast_bomb_09	HIDDEN_WARP_NPC,10,10
1@glast,132,289,0	duplicate(#glast_bomb_01)	#glast_bomb_10	HIDDEN_WARP_NPC,10,10
1@glast,199,115,0	duplicate(#glast_bomb_01)	#glast_bomb_11	HIDDEN_WARP_NPC,10,10
1@glast,139,46,0	duplicate(#glast_bomb_01)	#glast_bomb_12	HIDDEN_WARP_NPC,10,10
1@glast,82,86,0	duplicate(#glast_bomb_01)	#glast_bomb_13	HIDDEN_WARP_NPC,10,10
1@glast,68,226,0	duplicate(#glast_bomb_01)	#glast_bomb_14	HIDDEN_WARP_NPC,10,10

// Back to entrance
// event in #glast_move_01
1@glast,359,294,4	script	芬里尔#glast_11	4_F_FENRIR,{
	if (isbegin_quest(9337) == 0) {
		cutin "fenrir_a.bmp",2;
		mes "[芬里尔]";
		mes "你平安到达了！危险的局势使我担心。这是一项困难而危险的工作，我非常感谢你的帮助。";
		next;
		mes "[Fenrir]";
		mes "我觉得我们会再次见面，但也许只是我的想象？采购经理..那太好了。";
		next;
		mes "[Fenrir]";
		mes "我们一起工作得很好，所以我希望我们能再见面。注意莎拉，伤害也会降临到你的身上。";
		setquest 9337;// Wrapping up the Adventure
		getitem 607,1;// Yggdrasilberry
		getitem 608,1;// Seed_Of_Yggdrasil
		close2;
		cutin "",255;
		end;
	}
	mes "[Fenrir]";
	mes "我们一起工作得很好，所以我希望我们能再见面。";
	close;
}

1@glast,353,290,8	duplicate(#effect_glast_event01_1)	莎拉艾琳's alter ego	4_F_SARAH

// Shards Events
1@glast,290,147,4	script	Ancient 巨人#glast_1	4_GIGANTES_BIG,6,6,{
	end;
OnTouch:
	disablenpc instance_npcname("Ancient 巨人#glast_1");
	.@label$ = instance_npcname("Ancient 巨人#glast_1") + "::OnMobDead";
	monster 'map_glast$,290,147, "Ancient 巨人#X1",3196,1, .@label$;		// MM_L_GIGAN3
	monster 'map_glast$,290,146, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	monster 'map_glast$,288,147, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	unittalk getcharid(3), "" + strcharinfo(0) + " : The Ancient 巨人 that 班哈德博士 spoke of!!";
	enablenpc instance_npcname("Stone Gargoyle#glast_01");
	enablenpc instance_npcname("Stone Gargoyle#glast_02");
	enablenpc instance_npcname("Stone Gargoyle#glast_03");
	enablenpc instance_npcname("Stone Gargoyle#glast_04");
	enablenpc instance_npcname("Stone Gargoyle#glast_05");
	enablenpc instance_npcname("Stone Gargoyle#glast_06");
	end;
OnMobDead:
	end;
}

1@glast,300,248,4	script	Ancient 巨人#glast_2	4_GIGANTES_BIG,4,4,{
	end;
OnTouch:
	disablenpc instance_npcname("Ancient 巨人#glast_2");
	.@label$ = instance_npcname("Ancient 巨人#glast_2") + "::OnMobDead";
	monster 'map_glast$,300,248, "Ancient 巨人#X2",3196,1, .@label$;		// MM_L_GIGAN3
	monster 'map_glast$,299,250, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	monster 'map_glast$,302,246, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	unittalk getcharid(3), "" + strcharinfo(0) + " : The Ancient 巨人 that 班哈德博士 spoke of!!";
	enablenpc instance_npcname("Stone Gargoyle#glast_07");
	enablenpc instance_npcname("Stone Gargoyle#glast_08");
	enablenpc instance_npcname("Stone Gargoyle#glast_09");
	enablenpc instance_npcname("Stone Gargoyle#glast_10");
	enablenpc instance_npcname("Stone Gargoyle#glast_11");
	end;
OnMobDead:
	end;
}

1@glast,292,344,4	script	Ancient 巨人#glast_3	4_GIGANTES_BIG,6,6,{
	end;
OnTouch:
	disablenpc instance_npcname("Ancient 巨人#glast_3");
	.@label$ = instance_npcname("Ancient 巨人#glast_3") + "::OnMobDead";
	monster 'map_glast$,292,344, "Ancient 巨人#X3",3196,1, .@label$;		// MM_L_GIGAN3
	monster 'map_glast$,290,346, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	monster 'map_glast$,291,344, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	unittalk getcharid(3), "" + strcharinfo(0) + " : The Ancient 巨人 that 班哈德博士 spoke of!!";
	enablenpc instance_npcname("Stone Gargoyle#glast_12");
	enablenpc instance_npcname("Stone Gargoyle#glast_13");
	enablenpc instance_npcname("Stone Gargoyle#glast_14");
	end;
OnMobDead:
	end;
}

1@glast,107,147,6	script	Ancient 巨人#glast_4	4_GIGANTES_BIG,6,6,{
	end;
OnTouch:
	disablenpc instance_npcname("Ancient 巨人#glast_4");
	.@label$ = instance_npcname("Ancient 巨人#glast_4") + "::OnMobDead";
	monster 'map_glast$,107,147, "Ancient 巨人#X4",3196,1, .@label$;		// MM_L_GIGAN3
	monster 'map_glast$,107,145, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	monster 'map_glast$,106,148, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	unittalk getcharid(3), "" + strcharinfo(0) + " : The Ancient 巨人 that 班哈德博士 spoke of!!";
	enablenpc instance_npcname("Stone Gargoyle#glast_15");
	enablenpc instance_npcname("Stone Gargoyle#glast_16");
	enablenpc instance_npcname("Stone Gargoyle#glast_17");
	enablenpc instance_npcname("Stone Gargoyle#glast_18");
	enablenpc instance_npcname("Stone Gargoyle#glast_19");
	enablenpc instance_npcname("Stone Gargoyle#glast_20");
	end;
OnMobDead:
	end;
}

1@glast,98,248,6	script	Ancient 巨人#glast_5	4_GIGANTES_BIG,4,4,{
	end;
OnTouch:
	disablenpc instance_npcname("Ancient 巨人#glast_5");
	.@label$ = instance_npcname("Ancient 巨人#glast_5") + "::OnMobDead";
	monster 'map_glast$,98,248, "Ancient 巨人#X5",3196,1, .@label$;		// MM_L_GIGAN3
	monster 'map_glast$,96,250, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	monster 'map_glast$,98,247, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	unittalk getcharid(3), "" + strcharinfo(0) + " : The Ancient 巨人 that 班哈德博士 spoke of!!";
	enablenpc instance_npcname("Stone Gargoyle#glast_21");
	enablenpc instance_npcname("Stone Gargoyle#glast_22");
	enablenpc instance_npcname("Stone Gargoyle#glast_23");
	enablenpc instance_npcname("Stone Gargoyle#glast_24");
	enablenpc instance_npcname("Stone Gargoyle#glast_25");
	end;
OnMobDead:
	end;
}

1@glast,107,344,6	script	Ancient 巨人#glast_6	4_GIGANTES_BIG,6,6,{
	end;
OnTouch:
	disablenpc instance_npcname("Ancient 巨人#glast_6");
	.@label$ = instance_npcname("Ancient 巨人#glast_6") + "::OnMobDead";
	monster 'map_glast$,107,344, "Ancient 巨人#X6",3196,1, .@label$;		// MM_L_GIGAN3
	monster 'map_glast$,107,344, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	monster 'map_glast$,109,346, "Ancient Medium 巨人",3193,1, .@label$;	// MM_M_GIGAN3
	unittalk getcharid(3), "" + strcharinfo(0) + " : The Ancient 巨人 that 班哈德博士 spoke of!!";
	enablenpc instance_npcname("Stone Gargoyle#glast_26");
	enablenpc instance_npcname("Stone Gargoyle#glast_27");
	enablenpc instance_npcname("Stone Gargoyle#glast_28");
	end;
OnMobDead:
	end;
}

1@glast,277,130,8	script	Stone Gargoyle#glast_01	4_GARGOYLE_STATUE,2,2,{
	end;
OnTouch:
	getmapxy .@map$,.@x,.@y, BL_NPC;
	monster .@map$,.@x,.@y, "Mutant Gargoyle",3197,1, instance_npcname( strnpcinfo(0) ) + "::OnMobDead";	// MM_M_GARGOYLE
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
OnMobDead:
	end;
}

1@glast,279,130,8	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_02	4_GARGOYLE_STATUE,2,2
1@glast,281,130,8	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_03	4_GARGOYLE_STATUE,2,2
1@glast,277,161,6	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_04	4_GARGOYLE_STATUE,2,2
1@glast,279,161,6	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_05	4_GARGOYLE_STATUE,2,2
1@glast,281,161,6	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_06	4_GARGOYLE_STATUE,2,2
1@glast,277,250,6	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_07	4_GARGOYLE_STATUE,2,2
1@glast,277,245,8	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_08	4_GARGOYLE_STATUE,2,2
1@glast,272,248,6	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_09	4_GARGOYLE_STATUE,2,2
1@glast,273,260,4	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_10	4_GARGOYLE_STATUE,2,2
1@glast,273,235,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_11	4_GARGOYLE_STATUE,2,2
1@glast,277,328,8	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_12	4_GARGOYLE_STATUE,2,2
1@glast,279,328,8	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_13	4_GARGOYLE_STATUE,2,2
1@glast,281,328,8	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_14	4_GARGOYLE_STATUE,2,2
1@glast,122,132,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_15	4_GARGOYLE_STATUE,2,2
1@glast,120,132,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_16	4_GARGOYLE_STATUE,2,2
1@glast,118,132,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_17	4_GARGOYLE_STATUE,2,2
1@glast,118,163,4	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_18	4_GARGOYLE_STATUE,2,2
1@glast,120,163,4	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_19	4_GARGOYLE_STATUE,2,2
1@glast,118,163,4	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_20	4_GARGOYLE_STATUE,2,2
1@glast,122,250,4	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_21	4_GARGOYLE_STATUE,2,2
1@glast,122,245,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_22	4_GARGOYLE_STATUE,2,2
1@glast,126,248,4	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_23	4_GARGOYLE_STATUE,2,2
1@glast,126,260,6	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_24	4_GARGOYLE_STATUE,2,2
1@glast,126,235,8	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_25	4_GARGOYLE_STATUE,2,2
1@glast,117,328,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_26	4_GARGOYLE_STATUE,2,2
1@glast,119,328,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_27	4_GARGOYLE_STATUE,2,2
1@glast,121,328,2	duplicate(Stone Gargoyle#glast_01)	Stone Gargoyle#glast_28	4_GARGOYLE_STATUE,2,2


1@glast,1,1,0	script	#fenrir_spawn_chimera	HIDDEN_WARP_NPC,{
	end;
OnSpawn:
	// Right side
	monster 'map_glast$,351,259,"Wicked Chimera#bb28",3200,1;
	monster 'map_glast$,353,250,"Wicked Chimera#bb28",3200,1;
	monster 'map_glast$,351,243,"Wicked Chimera#bb28",3200,1;
	monster 'map_glast$,351,246,"Wicked Chimera#bb28",3200,1;
	monster 'map_glast$,355,248,"Wicked Chimera#bb28",3200,1;
	monster 'map_glast$,360,245,"Wicked Chimera#bb28",3200,1;

	monster 'map_glast$,298,209,"Wicked Chimera#aa14",3200,1;
	monster 'map_glast$,303,199,"Wicked Chimera#aa14",3200,1;
	monster 'map_glast$,298,198,"Wicked Chimera#aa14",3200,1;
	monster 'map_glast$,295,211,"Wicked Chimera#aa14",3200,1;

	monster 'map_glast$,307,163,"Wicked Chimera#bb15",3200,1;
	monster 'map_glast$,315,158,"Wicked Chimera#bb15",3200,1;
	monster 'map_glast$,318,163,"Wicked Chimera#bb15",3200,1;
	monster 'map_glast$,318,169,"Wicked Chimera#bb15",3200,1;

	monster 'map_glast$,337,119,"Wicked Chimera#bb27",3200,1;
	monster 'map_glast$,344,115,"Wicked Chimera#bb27",3200,1;
	monster 'map_glast$,353,117,"Wicked Chimera#bb27",3200,1;
	monster 'map_glast$,352,121,"Wicked Chimera#bb27",3200,1;
	monster 'map_glast$,345,114,"Wicked Chimera#bb27",3200,1;
	monster 'map_glast$,346,115,"Wicked Chimera#bb27",3200,1;

	monster 'map_glast$,264,36,"Wicked Chimera#aa26",3200,1;
	monster 'map_glast$,257,46,"Wicked Chimera#aa26",3200,1;
	monster 'map_glast$,253,44,"Wicked Chimera#aa26",3200,1;
	monster 'map_glast$,256,39,"Wicked Chimera#aa26",3200,1;

	// Bottom, left
	monster 'map_glast$,207,46,"Wicked Chimera#aa1",3200,1;
	monster 'map_glast$,197,49,"Wicked Chimera#aa1",3200,1;
	monster 'map_glast$,190,52,"Wicked Chimera#aa1",3200,1;
	monster 'map_glast$,200,64,"Wicked Chimera#aa1",3200,1;
	monster 'map_glast$,206,76,"Wicked Chimera#aa2",3200,1;
	monster 'map_glast$,207,83,"Wicked Chimera#aa2",3200,1;
	monster 'map_glast$,186,82,"Wicked Chimera#bb3",3200,1;
	monster 'map_glast$,200,87,"Wicked Chimera#aa2",3200,1;
	monster 'map_glast$,188,86,"Wicked Chimera#bb3",3200,1;
	monster 'map_glast$,215,77,"Wicked Chimera#aa2",3200,1;
	monster 'map_glast$,183,90,"Wicked Chimera#bb3",3200,1;
	monster 'map_glast$,192,85,"Wicked Chimera#bb3",3200,1;
	monster 'map_glast$,154,36,"Wicked Chimera#bb35",3200,1;
	monster 'map_glast$,150,47,"Wicked Chimera#bb35",3200,1;
	monster 'map_glast$,138,52,"Wicked Chimera#bb35",3200,1;
	monster 'map_glast$,152,36,"Wicked Chimera#bb35",3200,1;
	monster 'map_glast$,105,50,"Wicked Chimera#aa34",3200,1;
	monster 'map_glast$,100,48,"Wicked Chimera#aa34",3200,1;
	monster 'map_glast$,106,52,"Wicked Chimera#aa34",3200,1;
	monster 'map_glast$,82,52,"Wicked Chimera#aa33",3200,1;
	monster 'map_glast$,85,51,"Wicked Chimera#aa33",3200,1;
	monster 'map_glast$,80,65,"Wicked Chimera#aa33",3200,1;
	monster 'map_glast$,79,58,"Wicked Chimera#aa33",3200,1;
	monster 'map_glast$,82,69,"Wicked Chimera#aa33",3200,1;
	monster 'map_glast$,97,41,"Wicked Chimera#aa34",3200,1;
	monster 'map_glast$,86,103,"Wicked Chimera#bb32",3200,1;
	monster 'map_glast$,82,102,"Wicked Chimera#bb32",3200,1;
	monster 'map_glast$,78,108,"Wicked Chimera#bb32",3200,1;
	monster 'map_glast$,85,109,"Wicked Chimera#bb32",3200,1;
	monster 'map_glast$,82,111,"Wicked Chimera#bb32",3200,1;
	monster 'map_glast$,80,144,"Wicked Chimera#bb31",3200,1;
	monster 'map_glast$,72,152,"Wicked Chimera#bb31",3200,1;
	monster 'map_glast$,88,159,"Wicked Chimera#bb31",3200,1;
	monster 'map_glast$,73,152,"Wicked Chimera#bb31",3200,1;
	monster 'map_glast$,83,160,"Wicked Chimera#bb31",3200,1;
	monster 'map_glast$,77,150,"Wicked Chimera#bb31",3200,1;
	monster 'map_glast$,62,207,"Wicked Chimera#aa30",3200,1;
	monster 'map_glast$,70,215,"Wicked Chimera#aa30",3200,1;
	monster 'map_glast$,58,209,"Wicked Chimera#aa30",3200,1;
	monster 'map_glast$,68,204,"Wicked Chimera#aa30",3200,1;
	monster 'map_glast$,67,214,"Wicked Chimera#aa30",3200,1;
	monster 'map_glast$,58,250,"Wicked Chimera#aa29",3200,1;
	monster 'map_glast$,64,257,"Wicked Chimera#aa29",3200,1;
	monster 'map_glast$,70,251,"Wicked Chimera#aa29",3200,1;
	monster 'map_glast$,65,247,"Wicked Chimera#aa29",3200,1;
	monster 'map_glast$,65,256,"Wicked Chimera#aa29",3200,1;
	monster 'map_glast$,60,255,"Wicked Chimera#aa29",3200,1;
	monster 'map_glast$,50,144,"Wicked Chimera#bb36",3200,1;
	monster 'map_glast$,52,143,"Wicked Chimera#bb36",3200,1;
	monster 'map_glast$,54,146,"Wicked Chimera#bb36",3200,1;
	monster 'map_glast$,55,140,"Wicked Chimera#bb36",3200,1;

	// Left inside
	monster 'map_glast$,177,88,"Wicked Chimera#bb4",3200,1;
	monster 'map_glast$,163,108,"Wicked Chimera#bb4",3200,1;
	monster 'map_glast$,147,113,"Wicked Chimera#bb4",3200,1;
	monster 'map_glast$,136,179,"Wicked Chimera#bb11",3200,1;
	monster 'map_glast$,136,181,"Wicked Chimera#bb11",3200,1;
	monster 'map_glast$,125,190,"Wicked Chimera#bb11",3200,1;
	monster 'map_glast$,134,200,"Wicked Chimera#bb12",3200,1;
	monster 'map_glast$,134,190,"Wicked Chimera#bb11",3200,1;
	monster 'map_glast$,127,203,"Wicked Chimera#bb12",3200,1;
	monster 'map_glast$,151,206,"Wicked Chimera#aa13",3200,1;
	monster 'map_glast$,125,209,"Wicked Chimera#bb12",3200,1;
	monster 'map_glast$,134,204,"Wicked Chimera#bb12",3200,1;
	monster 'map_glast$,135,244,"Wicked Chimera#bb19",3200,1;
	monster 'map_glast$,149,250,"Wicked Chimera#bb19",3200,1;
	monster 'map_glast$,138,249,"Wicked Chimera#bb19",3200,1;
	monster 'map_glast$,136,251,"Wicked Chimera#bb19",3200,1;
	monster 'map_glast$,127,291,"Wicked Chimera#bb20",3200,1;
	monster 'map_glast$,126,288,"Wicked Chimera#bb20",3200,1;
	monster 'map_glast$,124,291,"Wicked Chimera#bb20",3200,1;
	monster 'map_glast$,135,309,"Wicked Chimera#aa21",3200,1;
	monster 'map_glast$,138,316,"Wicked Chimera#aa21",3200,1;
	monster 'map_glast$,139,324,"Wicked Chimera#aa22",3200,1;
	monster 'map_glast$,132,331,"Wicked Chimera#aa22",3200,1;
	monster 'map_glast$,129,315,"Wicked Chimera#aa21",3200,1;
	monster 'map_glast$,127,338,"Wicked Chimera#aa22",3200,1;
	monster 'map_glast$,126,327,"Wicked Chimera#aa22",3200,1;
	monster 'map_glast$,132,288,"Wicked Chimera#bb20",3200,1;
	monster 'map_glast$,138,309,"Wicked Chimera#aa21",3200,1;

	// Center and right side
	monster 'map_glast$,158,214,"Wicked Chimera#aa13",3200,1;
	monster 'map_glast$,162,208,"Wicked Chimera#aa13",3200,1;
	monster 'map_glast$,166,215,"Wicked Chimera#aa13",3200,1;
	monster 'map_glast$,168,191,"Wicked Chimera#aa10",3200,1;
	monster 'map_glast$,164,186,"Wicked Chimera#aa10",3200,1;
	monster 'map_glast$,165,191,"Wicked Chimera#aa10",3200,1;
	monster 'map_glast$,177,189,"Wicked Chimera#aa10",3200,1;
	monster 'map_glast$,199,200,"Wicked Chimera#aa9",3200,1;
	monster 'map_glast$,205,195,"Wicked Chimera#aa9",3200,1;
	monster 'map_glast$,191,200,"Wicked Chimera#aa9",3200,1;
	monster 'map_glast$,196,192,"Wicked Chimera#aa9",3200,1;
	monster 'map_glast$,224,196,"Wicked Chimera#bb8",3200,1;
	monster 'map_glast$,233,196,"Wicked Chimera#bb8",3200,1;
	monster 'map_glast$,239,192,"Wicked Chimera#bb8",3200,1;
	monster 'map_glast$,231,193,"Wicked Chimera#bb8",3200,1;
	monster 'map_glast$,239,214,"Wicked Chimera#bb16",3200,1;
	monster 'map_glast$,232,213,"Wicked Chimera#bb16",3200,1;
	monster 'map_glast$,238,209,"Wicked Chimera#bb16",3200,1;
	monster 'map_glast$,244,220,"Wicked Chimera#bb16",3200,1;
	monster 'map_glast$,252,249,"Wicked Chimera#aa18",3200,1;
	monster 'map_glast$,268,221,"Wicked Chimera#aa17",3200,1;
	monster 'map_glast$,261,219,"Wicked Chimera#aa17",3200,1;
	monster 'map_glast$,261,248,"Wicked Chimera#aa18",3200,1;
	monster 'map_glast$,264,211,"Wicked Chimera#aa17",3200,1;
	monster 'map_glast$,267,210,"Wicked Chimera#aa17",3200,1;
	monster 'map_glast$,258,193,"Wicked Chimera#bb7",3200,1;
	monster 'map_glast$,258,192,"Wicked Chimera#bb7",3200,1;
	monster 'map_glast$,258,190,"Wicked Chimera#bb7",3200,1;
	monster 'map_glast$,269,192,"Wicked Chimera#bb7",3200,1;
	monster 'map_glast$,269,110,"Wicked Chimera#aa6",3200,1;
	monster 'map_glast$,266,105,"Wicked Chimera#aa6",3200,1;
	monster 'map_glast$,277,103,"Wicked Chimera#aa6",3200,1;
	monster 'map_glast$,263,107,"Wicked Chimera#aa6",3200,1;
	monster 'map_glast$,249,101,"Wicked Chimera#aa5",3200,1;
	monster 'map_glast$,237,93,"Wicked Chimera#aa5",3200,1;
	monster 'map_glast$,242,107,"Wicked Chimera#aa5",3200,1;
	monster 'map_glast$,237,105,"Wicked Chimera#aa5",3200,1;
	monster 'map_glast$,264,235,"Wicked Chimera#aa18",3200,1;
	monster 'map_glast$,260,255,"Wicked Chimera#aa18",3200,1;
	monster 'map_glast$,261,282,"Wicked Chimera#bb23",3200,1;
	monster 'map_glast$,260,284,"Wicked Chimera#bb23",3200,1;
	monster 'map_glast$,264,289,"Wicked Chimera#bb23",3200,1;
	monster 'map_glast$,268,301,"Wicked Chimera#bb24",3200,1;
	monster 'map_glast$,262,308,"Wicked Chimera#bb24",3200,1;
	monster 'map_glast$,273,306,"Wicked Chimera#bb24",3200,1;
	monster 'map_glast$,259,307,"Wicked Chimera#bb24",3200,1;
	monster 'map_glast$,269,283,"Wicked Chimera#bb23",3200,1;
	monster 'map_glast$,257,350,"Wicked Chimera#aa25",3200,1;
	monster 'map_glast$,268,350,"Wicked Chimera#aa25",3200,1;
	monster 'map_glast$,268,357,"Wicked Chimera#aa25",3200,1;
	monster 'map_glast$,271,358,"Wicked Chimera#aa25",3200,1;
	end;

OnInstanceInit:
	'skip_left_part = 0;
	'back_to_entrance = 0;
	'map_glast$ = instance_mapname("1@glast");
	'fenrir_1$ = instance_npcname("芬里尔#glast_01");
	'fenrir_2$ = instance_npcname("芬里尔#glast_02");
	'fenrir_3$ = instance_npcname("芬里尔#glast_03");
	'fenrir_4$ = instance_npcname("芬里尔#glast_04");
	'fenrir_5$ = instance_npcname("芬里尔#glast_05");
	'fenrir_6$ = instance_npcname("芬里尔#glast_06");
	'fenrir_7$ = instance_npcname("芬里尔#glast_07");
	'fenrir_8$ = instance_npcname("芬里尔#glast_08");
	'fenrir_9$ = instance_npcname("芬里尔#glast_09");
	'fenrir_10$ = instance_npcname("芬里尔#glast_10");
	'fenrir_11$ = instance_npcname("芬里尔#glast_11");
	'sarah_10$ = instance_npcname("莎拉艾琳#glast_10");
	'sarah_17$ = instance_npcname("莎拉艾琳#glast_17");
	'sarah_20$ = instance_npcname("莎拉艾琳#glast_20");
	'sarah_ego$ = instance_npcname("莎拉艾琳's alter ego");

	// Entrance
	disablenpc instance_npcname("#glast_event_3");
	for ( .@i = 1; .@i < 8; .@i++ )
		hideonnpc instance_npcname("#effect_glast_event01_" + .@i);
	disablenpc instance_npcname("#glast_move_01");

	// Fenrir bottom
	disablenpc instance_npcname("#glast_event_5");
	disablenpc 'fenrir_2$;

	// Fenrir left side, entrance
	disablenpc instance_npcname("#glast_event_7");
	disablenpc 'fenrir_3$;

	// Fenrir left side, inside
	disablenpc 'fenrir_4$;
	disablenpc 'sarah_10$;
	disablenpc instance_npcname("巨人#semi1");
	disablenpc instance_npcname("巨人#semi2");
	disablenpc instance_npcname("#glast_event_9");
	for ( .@i = 1; .@i < 9; .@i++ )
		hideonnpc instance_npcname("#effect_glast_event02_" + .@i);
	hideonnpc instance_npcname("#effect_glast_event02_bis");
	//for ( .@i = 1; .@i < 8; .@i++ )
	//	disablenpc instance_npcname("#glast_event_warp_" + .@i);
	disablenpc instance_npcname("#fenrir_left_vermilion");
	disablenpc instance_npcname("#fenrir_left_final_1");

	// Event top side
	disablenpc instance_npcname("#glast_event_13");
	disablenpc 'fenrir_5$;

	// Skipped left side
	disablenpc instance_npcname("#fenrir_left_final_2");

	// Stairs final room
	disablenpc instance_npcname("#glast_move_03");
	disablenpc instance_npcname("#glast_event_15");
	disablenpc 'fenrir_6$;

	// Final room
	disablenpc instance_npcname("#glast_move_03a");
	disablenpc instance_npcname("#glast_event_18");
	disablenpc instance_npcname("莎拉艾琳#glast_17");
	disablenpc 'sarah_20$;
	disablenpc 'fenrir_7$;
	disablenpc 'fenrir_8$;
	disablenpc 'fenrir_9$;
	//for ( .@i = 11; .@i <= 16; .@i++ )
	//	disablenpc instance_npcname("#glast_event_warp_" + .@i);
	for ( .@i = 1; .@i <= 8; .@i++ )
		hideonnpc instance_npcname("#effect_glast_event03_" + .@i);
	disablenpc instance_npcname("#glast_event_19");
	disablenpc instance_npcname("#fenrir_boss_final_1");

	// Outside final room
	disablenpc instance_npcname("#glast_event_24");
	disablenpc 'fenrir_10$;

	// Skipped final room
	disablenpc instance_npcname("#fenrir_boss_final_2");

	// Bombs
	disablenpc instance_npcname("#sarah_wrath");
	for ( .@i = 1; .@i <= 14; .@i++ )
		disablenpc instance_npcname("#glast_bomb_" + (.@i < 10 ? ("0"+.@i) : .@i));

	// Back to entrance
	disablenpc instance_npcname("莎拉艾琳's alter ego");
	disablenpc 'fenrir_11$;
	disablenpc instance_npcname("#glast_move_04");
	disablenpc instance_npcname("#glast_move_05");

	// Shards
	for ( .@i = 1; .@i < 7; .@i++ )
		disablenpc instance_npcname("Ancient 巨人#glast_" + .@i);
	for ( .@i = 1; .@i < 29; .@i++ )
		disablenpc instance_npcname( "Stone Gargoyle#glast_" + (.@i < 10 ? ("0"+.@i) : .@i) );

	// Spawn chimera
	donpcevent instance_npcname("#fenrir_spawn_chimera") + "::OnSpawn";
	end;
}
