//===== rAthena Script =======================================
//= Advanced Refiner
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Changelog: ===========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//= 1.5 Some official script updates. [Euphy]
//= 1.6 Added VIP features. [Euphy]
//= 1.7 Removed re-roll behavior. [Secret]
//============================================================

payon,157,146,6	script	龙先生#cash	85,{
	disable_items;
	mes "[龙先生]";
	mes "我是专门精炼武器与防具的铁匠.";
	mes "能为你精炼穿在身上的装备.";
	mes "你想精炼哪一项物品呢?";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@indices[.@i])) {
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@equipped,1;
		}
		else
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[没有装备]";
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[龙先生]";
		mes "你好歹倒是把装备穿上先...";
		close;
	}
	set .@part, .@indices[ select(.@menu$) ];

	if(!getequipisequiped(.@part)) { //custom check
		mes "[龙先生]";
		mes "要不要我把你的头脑精炼得";
		mes "聪明一点啊?";
		emotion ET_FRET;
		close;
	}
	if (!getequipisenableref(.@part)) {
		mes "[龙先生]";
		mes "你选中的这个装备是无法精炼的道具喔!";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[龙先生]";
		mes "这件装备已经精炼到10级了.";
		mes "以我的能力, 已经无法再继续锻造了..";
		close;
	}

	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	.@price = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);

	// Make sure you have the necessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	callsub S_RefineValidate,getequipweaponlv(.@part),.@material,.@price,.@part,.@refineitemid,.@refinerycnt;

	mes "[龙先生]";
	mes "锵! 锵! 锵!";
	if (getequippercentrefinery(.@part, true) > rand(100)) {
		successrefitem .@part;
		next;
		emotion ET_BEST;
		mes "[龙先生]";
		mes "拿去吧! 已经做好了.";
		mes "好久没精炼出这么优质的装备了,";
		mes ((getequipweaponlv(.@part))?"武器":"防具") + "变强了你一定也很高兴吧!";
		close;
	}
	failedrefitem .@part;
	next;
	emotion (!rand(5))?ET_MONEY:ET_HUK;
	mes "[龙先生]";
	mes "咳咳咳咳咳咳咳!!!!";
	next;
	mes "[龙先生]";
	mes "咳...咳咳...真...丢脸.";
	mes "武器在精炼的过程中损坏了.";
	mes "我...我...";
	mes "我早就告诉过你会这样的嘛!";
	close;

S_RefineValidate:
	.@weapon_lvl = getarg(0);
	.@item_req = getarg(1);
	.@price = getarg(2);
	.@part = getarg(3);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		switch(.@weapon_lvl){
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
		}
	}

	mes "[龙先生]";
	if (.@weapon_lvl)
		mes "这把武器的等级为 ^ff0000" + .@weapon_lvl + " ^000000级!";
	mes "精练这个物品我需要:";
	mes "- ^0000ff"+getitemname(.@item_req)+"^000000 x 1";
	mes "- 精炼服务费 ^ff0000"+ .@price + "^000000 Zeny";
	mes "你真的希望我继续吗?";
	next;
	if( select("是的:不要") == 1 ) {
		if (getequippercentrefinery(.@part) < 100) {
			if (.@weapon_lvl) {
				mes "[龙先生]";
				mes "呜哇!!";
				mes "看来这武器已经精炼很多次了的样子...";
				mes "要是再继续精炼的话, 可能会造成损坏喔!";
				next;
				mes "要是损坏的话可就不能继续使用了!!";
				mes "插在里面的卡片和特征";
				mes "^ff0000全部都会消失了!!^000000";
				mes "再说一次, 损坏的话装备会消失!!";
				mes "你确定还要继续精炼吗?";
				next;
				if(select("是:否") == 2) {
					mes "[龙先生]";
					mes "很好.";
					mes "如果太过鲁莽得精炼, 而造成装备损坏, 我心情也会跟着变坏的.";
					close;
				}
			} else {
				mes "[龙先生]";
				mes "咯咯咯~~ 噢, 你还真有胆量.";
				mes "你这件防具已经精炼很多次了的样子...";
				mes "要是再继续精炼的话, 可能会造成损坏喔!";
				next;
				mes "要是损坏的话可就不能继续使用了!!";
				mes "插在里面的卡片和好不容易提升的精炼值";
				mes "^ff0000全部都会消失了!!^000000";
				mes "再说一次, 损坏的话装备会消失!!";
				mes "你确定还要继续精炼吗?";
				next;
				if(select("是:否") == 2) {
					mes "[龙先生]";
					mes "你这是在浪费我宝贵的时间!!";
					mes "从我眼前消失, 胆小鬼.";
					close;
				}
			}
		}
		if (countitem(.@item_req) > 0 && Zeny > .@price) {
			delitem .@item_req,1;
			set Zeny, Zeny - .@price;

			// anti-hack
			if (callfunc("F_IsEquipIDHack", .@part, getarg(4)) ||
				callfunc("F_IsEquipRefineHack", .@part, getarg(5)) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3])) {
				mes "[Holink]";
				emotion ET_FRET;
				mes "Wait a second...";
				mes "Do you think I'm stupid?!";
				mes "You switched the item while I wasn't looking! Get out of here!";
				close;
			}

			return;
		}
		mes "[龙先生]";
		mes "这就是你带来的东西?";
		mes "我很抱歉, 没有材料以及我的酬金, 我什么也做不了的.";
		close;
	}
	mes "[龙先生]";
	mes "如果你不乐意的话, 那我也没办法...";
	close;
}
